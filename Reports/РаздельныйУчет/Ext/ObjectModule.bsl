#Область ДляВызоваИзДругихПодсистем

// Задать настройки формы отчета.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//         - Неопределено
//   КлючВарианта - Строка
//                - Неопределено
//   Настройки - см. ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойВариантаНаСервере = Истина;
		
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
// См. также ФормаКлиентскогоПриложения.ПриСозданииНаСервере в синтакс-помощнике.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	МодульОбработчикКоманд = "КЛАДО_ОтчетРаздельныйУчетКлиент.ОбработчикКоманды_";
	
	// Создание и вывод команд
	КомандыГруппыПланФакт = Новый Массив;
		
	КомандаПлан = Форма.Команды.Добавить("КЛАДО_План");
	КомандаПлан.Заголовок = "План";
	КомандаПлан.Действие = МодульОбработчикКоманд + "План";	
	КомандыГруппыПланФакт.Добавить(КомандаПлан);
	
	КомандаФакт = Форма.Команды.Добавить("КЛАДО_Факт");
	КомандаФакт.Заголовок = "Факт";
	КомандаФакт.Действие = МодульОбработчикКоманд + "Факт";	
	КомандыГруппыПланФакт.Добавить(КомандаФакт);
	
	ОтчетыСервер.ВывестиКоманду(Форма, КомандыГруппыПланФакт, "Главное", , , "_КЛАДО_ГруппаПланФакт");
	
	КомандыГруппыНДС = Новый Массив;
	                                     
	КомандаСНДС = Форма.Команды.Добавить("КЛАДО_СНДС");
	КомандаСНДС.Заголовок = "С НДС";
	КомандаСНДС.Действие = МодульОбработчикКоманд + "СНДС";
	КомандыГруппыНДС.Добавить(КомандаСНДС);
	
	КомандаБезНДС = Форма.Команды.Добавить("КЛАДО_БезНДС");
	КомандаБезНДС.Заголовок = "Без НДС";
	КомандаБезНДС.Действие = МодульОбработчикКоманд + "БезНДС";
	КомандыГруппыНДС.Добавить(КомандаБезНДС);
	
	ОтчетыСервер.ВывестиКоманду(Форма, КомандыГруппыНДС, "Главное", , Истина, "_КЛАДО_ГруппаНДС");
	
	// Создание дополнительных параметров на форме
	СтруктураДопПараметров = Новый Структура;
	СтруктураДопПараметров.Вставить("СуммыСНДС", Истина);
	СтруктураДопПараметров.Вставить("ОтображатьТолькоПлан", Ложь);
	
	КЛАДО_РаботаСЭлементамиФормы.СоздатьРеквизит(Форма, "КЛАДО_ДопПараметры", Новый ОписаниеТипов);
	
	Форма.КЛАДО_ДопПараметры = СтруктураДопПараметров;
	
	Форма.КЛАДО_ДопПараметры.ОтображатьТолькоПлан = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Форма.Параметры,
		"ОтображатьТолькоПлан", Ложь);
	
	Если ПараметрыСеанса.СтатусОтбор = 1 Тогда
		Форма.КЛАДО_ДопПараметры.ОтображатьТолькоПлан = Истина;
	КонецЕсли; 
	
	// Управление пометками команд
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
		"ГруппаГлавное_КЛАДО_ГруппаПланФакт", "Отображение", ОтображениеГруппыКнопок.Компактное);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
		"КЛАДО_План", "Пометка", Форма.КЛАДО_ДопПараметры.ОтображатьТолькоПлан);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
		"КЛАДО_Факт", "Пометка", Не Форма.КЛАДО_ДопПараметры.ОтображатьТолькоПлан);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
		"КЛАДО_СНДС", "Пометка", Истина);
	
КонецПроцедуры 

// Вызывается в обработчике одноименного события формы отчета и формы настройки отчета.
// См. "Расширение управляемой формы для отчета.ПередЗагрузкойВариантаНаСервере" в синтакс-помощнике.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма отчета или настроек отчета.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных - настройки для загрузки в компоновщик настроек.
//
Процедура ПередЗагрузкойВариантаНаСервере(Форма, НовыеНастройкиКД) Экспорт
	
	// Установка параметров данных
	ПараметрыДанных = НовыеНастройкиКД.ПараметрыДанных;
	
	Если Не ПараметрыДанных.Элементы.Найти("План") = Неопределено Тогда
		ПараметрыДанных.УстановитьЗначениеПараметра("План", Форма.КЛАДО_ДопПараметры.ОтображатьТолькоПлан);
	КонецЕсли;
	
	Если Не ПараметрыДанных.Элементы.Найти("Факт") = Неопределено Тогда
		ПараметрыДанных.УстановитьЗначениеПараметра("Факт", Не Форма.КЛАДО_ДопПараметры.ОтображатьТолькоПлан);
	КонецЕсли;
	
	Если Не ПараметрыДанных.Элементы.Найти("СуммыСНДС") = Неопределено Тогда
		ПараметрыДанных.УстановитьЗначениеПараметра("СуммыСНДС", Форма.КЛАДО_ДопПараметры.СуммыСНДС);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
