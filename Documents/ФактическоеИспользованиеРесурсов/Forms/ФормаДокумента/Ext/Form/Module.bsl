
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьФактЧасыПоТабелюНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ПерсоналПриИзменении(Элемент)
	
	ЧасыИзГрафика = ПолучитьЧасыИзГрафикаРаботы(Элементы.Персонал.ТекущиеДанные.Персонал);
	
	Если ЧасыИзГрафика <> 0 Тогда 
		Элементы.Персонал.ТекущиеДанные.Сумма = (Элементы.Персонал.ТекущиеДанные.КоличествоЧасов / ЧасыИзГрафика) * Элементы.Персонал.ТекущиеДанные.Цена;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыПриИзменении(Элемент)
	
	ТекущиеДанныеСтрока = Элементы.Материалы.ТекущиеДанные;
	
	Если ТекущиеДанныеСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанныеСтрока.Сумма = ТекущиеДанныеСтрока.Количество * ТекущиеДанныеСтрока.Цена;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФактЧасыПоТабелю(Команда)
	ЗаполнитьФактЧасыПоТабелюНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЦеныПерсонал(Команда)
	ЗаполнитьЦеныПерсоналНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПерсоналПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодчиненныеСтатьи(Команда)
	ЗаполнитьПодчиненныеСтатьиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СортировкаПерсонал(Команда)
	Объект.Персонал.Сортировать("Персонал, Договор, СтатьяЗатрат")
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервереБезКонтекста
Функция ПолучитьЧасыИзГрафикаРаботы(Персонал)
	Попытка
		Возврат Персонал.ГрафикРаботы.КоличествоЧасовВДень;	
	Исключение
		Возврат 0;
	КонецПопытки;
КонецФункции

Процедура ЗаполнитьФактЧасыПоТабелюНаСервере()
	
	СписокПерсонала = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Персонал, "Персонал", Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТабельУчетаИспользованияРабочегоВремениТабель.Персонал КАК Персонал,
		|	СУММА(ТабельУчетаИспользованияРабочегоВремениТабель.ВсегоОтработанноеКоличествоЧасов) КАК ВсегоОтработанноеКоличествоЧасов
		|ИЗ
		|	Документ.ТабельУчетаИспользованияРабочегоВремени.Табель КАК ТабельУчетаИспользованияРабочегоВремениТабель
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ТабельУчетаИспользованияРабочегоВремениТабель.Ссылка.Месяц, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Месяц, МЕСЯЦ)
		|	И ТабельУчетаИспользованияРабочегоВремениТабель.Персонал В(&Персонал)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТабельУчетаИспользованияРабочегоВремениТабель.Персонал";
	
	Запрос.УстановитьПараметр("Месяц", Объект.Месяц);
	Запрос.УстановитьПараметр("Персонал", СписокПерсонала);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Персонал", ВыборкаДетальныеЗаписи.Персонал);
		НайдСтроки = Объект.Персонал.НайтиСтроки(ПараметрыОтбора);
		Для Каждого НайдСтр Из НайдСтроки Цикл
			НайдСтр.ФактЧасыПоТабелю = ВыборкаДетальныеЗаписи.ВсегоОтработанноеКоличествоЧасов;	
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЦеныПерсоналНаСервере()
	Для Каждого Стр Из Объект.Персонал Цикл
		Стр.Цена = РегистрыСведений.ФактическиеЦеныНаРесурсы.ПолучитьЦенуРесурса(КонецМесяца(Объект.Месяц), Объект.Организация, Стр.Персонал, Стр.СтатьяЗатрат, Объект.Сценарий);
		Если Стр.ФактЧасыПоТабелю <> 0 Тогда 
			Стр.Сумма = (Стр.КоличествоЧасов/ Стр.ФактЧасыПоТабелю) * Стр.Цена;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодчиненныеСтатьиНаСервере()
	Для Каждого Стр Из Объект.Персонал Цикл
		Если Стр.СтатьяЗатрат.ЗависимыеСтатьиЗатрат.Количество() <> 0 Тогда
			Для Каждого СтрПодчиненная Из Стр.СтатьяЗатрат.ЗависимыеСтатьиЗатрат Цикл
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("Персонал", Стр.Персонал);
				ПараметрыОтбора.Вставить("Договор", Стр.Договор);
				ПараметрыОтбора.Вставить("СтатьяЗатрат", СтрПодчиненная.СтатьяЗатрат);
				НайдСтроки = Объект.Персонал.НайтиСтроки(ПараметрыОтбора);
				Если НайдСтроки.Количество() = 0 Тогда 
					СтрНовая = Объект.Персонал.Добавить();
					ЗаполнитьЗначенияСвойств(СтрНовая,Стр);
					СтрНовая.СтатьяЗатрат = СтрПодчиненная.СтатьяЗатрат;
					СтрНовая.Цена = 0;
					СтрНовая.Сумма = 0;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти