
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПериодЗаполнения.ДатаНачала		= Объект.НачалоПериода;
	ПериодЗаполнения.ДатаОкончания	= Объект.КонецПериода;
	
	ДобавитьВТаблицуКолонкиНаСервере();
	ЗаполнитьТаблицуНаФормеИзОбъекта();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ЗаполнитьТаблицуВОбъекте();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Персонал") Тогда 
		ДобавитьПерсоналНаСервере(ВыбранноеЗначение);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ПериодЗаполненияПриИзменении(Элемент)
	
	Если НЕ ТаблицаВремяНаФорме.Количество() = 0 Тогда 
		ПоказатьВопрос(Новый ОписаниеОповещения("ПериодЗаполненияПриИзмененииЗавершение", ЭтотОбъект), "При изменении периода таблица будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);
        Возврат;
	КонецЕсли;
	
	ПериодЗаполненияПриИзмененииФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодЗаполненияПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда 
		ПериодЗаполнения.ДатаНачала		= Объект.НачалоПериода;
		ПериодЗаполнения.ДатаОкончания	= Объект.КонецПериода;
		Возврат;
	КонецЕсли;
	
	ПериодЗаполненияПриИзмененииФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ПериодЗаполненияПриИзмененииФрагмент()
	
	Объект.НачалоПериода	= ПериодЗаполнения.ДатаНачала;
	Объект.КонецПериода		= ПериодЗаполнения.ДатаОкончания;
	
	ДобавитьВТаблицуКолонкиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВсе(Команда)
	КоличествоЧасов = 0;
	ПоказатьВводЧисла(Новый ОписаниеОповещения("ЗаполнитьВсеЗавершение", ЭтотОбъект, Новый Структура("КоличествоЧасов", КоличествоЧасов)), КоличествоЧасов,"Введите количество часов:");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВсеЗавершение(Число, ДополнительныеПараметры) Экспорт
	
	КоличествоЧасов = ?(Число = Неопределено, ДополнительныеПараметры.КоличествоЧасов, Число);
	
	Для Каждого Стр Из ТаблицаВремяНаФорме Цикл
		Для Каждого СтрКолонки Из СоответствиеПериодовНазванийКолонок Цикл 
			Стр[СтрКолонки.ИмяКолонки] = КоличествоЧасов;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыделенныеСтроки(Команда)
	КоличествоЧасов = 0;
	ПоказатьВводЧисла(Новый ОписаниеОповещения("ЗаполнитьВыделенныеСтрокиЗавершение", ЭтотОбъект, Новый Структура("КоличествоЧасов", КоличествоЧасов)), КоличествоЧасов,"Введите количество часов:");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыделенныеСтрокиЗавершение(Число, ДополнительныеПараметры) Экспорт
	
	КоличествоЧасов = ?(Число = Неопределено, ДополнительныеПараметры.КоличествоЧасов, Число);
	
	Для Каждого НомерСтр Из Элементы.ТаблицаВремяНаФорме.ВыделенныеСтроки Цикл
		Для Каждого СтрКолонки Из СоответствиеПериодовНазванийКолонок Цикл 
			ТаблицаВремяНаФорме.НайтиПоИдентификатору(НомерСтр)[СтрКолонки.ИмяКолонки] = КоличествоЧасов;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыделенныеЯчейки(Команда)
	КоличествоЧасов = 0;
	ПоказатьВводЧисла(Новый ОписаниеОповещения("ЗаполнитьВыделенныеЯчейкиЗавершение", ЭтотОбъект, Новый Структура("КоличествоЧасов", КоличествоЧасов)), КоличествоЧасов,"Введите количество часов:");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыделенныеЯчейкиЗавершение(Число, ДополнительныеПараметры) Экспорт
	
	КоличествоЧасов = ?(Число = Неопределено, ДополнительныеПараметры.КоличествоЧасов, Число);
	
	СписокМесяцев = Новый СписокЗначений;
	Для Каждого Стр Из СоответствиеПериодовНазванийКолонок Цикл
		СписокМесяцев.Добавить(Стр.ИмяКолонки,Формат(Стр.Месяц,"ДФ='MMMM yyyy'"));
	КонецЦикла;
	
	СписокМесяцев.ЗаполнитьПометки(Истина);
	СписокМесяцев.ПоказатьОтметкуЭлементов(Новый ОписаниеОповещения("ЗаполнитьВыделенныеЯчейкиЗавершениеЗавершение", ЭтотОбъект, Новый Структура("КоличествоЧасов", КоличествоЧасов)),"Выберите колонки для заполнения:");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыделенныеЯчейкиЗавершениеЗавершение(СписокМесяцев, ДополнительныеПараметры) Экспорт
	
	Если СписокМесяцев <> Неопределено Тогда 
		
		Для Каждого НомерСтр Из Элементы.ТаблицаВремяНаФорме.ВыделенныеСтроки Цикл
			Для Каждого СтрКолонки Из СписокМесяцев Цикл 
				Если СтрКолонки.Пометка Тогда 
					ТаблицаВремяНаФорме.НайтиПоИдентификатору(НомерСтр)[СтрКолонки.Значение] = ДополнительныеПараметры.КоличествоЧасов;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВсеИзГрафикаРаботы(Команда)
	
	Для Каждого Стр Из ТаблицаВремяНаФорме Цикл
		Для Каждого СтрКолонки Из СоответствиеПериодовНазванийКолонок Цикл 
			Стр[СтрКолонки.ИмяКолонки] = КоличествоЧасовИзПерсонала(Стр.Должность);
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыделенныеСтрокиИзГрафикаРаботы(Команда)
	
	Для Каждого НомерСтр Из Элементы.ТаблицаВремяНаФорме.ВыделенныеСтроки Цикл
		Для Каждого СтрКолонки Из СоответствиеПериодовНазванийКолонок Цикл 
			ТаблицаВремяНаФорме.НайтиПоИдентификатору(НомерСтр)[СтрКолонки.ИмяКолонки] = КоличествоЧасовИзПерсонала(ТаблицаВремяНаФорме.НайтиПоИдентификатору(НомерСтр).Должность);
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыделенныеЯчейкиИзГрафикаРаботы(Команда)
	
	СписокМесяцев = Новый СписокЗначений;
	Для Каждого Стр Из СоответствиеПериодовНазванийКолонок Цикл
		СписокМесяцев.Добавить(Стр.ИмяКолонки,Формат(Стр.Месяц,"ДФ='MMMM yyyy'"));
	КонецЦикла;
	
	СписокМесяцев.ЗаполнитьПометки(Истина);
	СписокМесяцев.ПоказатьОтметкуЭлементов(Новый ОписаниеОповещения("ЗаполнитьВыделенныеЯчейкиИзГрафикаРаботыЗавершение", ЭтотОбъект),"Выберите колонки для заполнения:");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыделенныеЯчейкиИзГрафикаРаботыЗавершение(СписокМесяцев, ДополнительныеПараметры) Экспорт
	
	Если СписокМесяцев <> Неопределено Тогда 
		
		Для Каждого НомерСтр Из Элементы.ТаблицаВремяНаФорме.ВыделенныеСтроки Цикл
			Для Каждого СтрКолонки Из СписокМесяцев Цикл 
				Если СтрКолонки.Пометка Тогда 
					ТаблицаВремяНаФорме.НайтиПоИдентификатору(НомерСтр)[СтрКолонки.Значение] = КоличествоЧасовИзПерсонала(ТаблицаВремяНаФорме.НайтиПоИдентификатору(НомерСтр).Должность);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры



&НаКлиенте
Процедура Подбор(Команда)
	
	СтруктураПараметры = Новый Структура("ЗакрыватьПриВыборе, РежимВыбора, ВыборГруппИЭлементов", Ложь, Истина, ИспользованиеГруппИЭлементов.ГруппыИЭлементы);	
	
	ОткрытьФорму("Справочник.Персонал.ФормаВыбора",СтруктураПараметры, ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьВТаблицуКолонкиНаСервере()
	
	ТаблицаВремяНаФормеЗначение = РеквизитФормыВЗначение("ТаблицаВремяНаФорме");
    
    МассивРеквизитов = Новый Массив;
    
	Для Каждого Колонка Из ТаблицаВремяНаФормеЗначение.Колонки Цикл
		Если СтрНайти(Колонка.Имя,"Месяц") <> 0 Тогда 
        	МассивРеквизитов.Добавить("ТаблицаВремяНаФорме." + Колонка.Имя);        
		КонецЕсли;
    КонецЦикла;
    ИзменитьРеквизиты(,МассивРеквизитов);
	
	Для Каждого СтрСоотвествие Из СоответствиеПериодовНазванийКолонок Цикл
		ТаблицаВремяНаФормеЗначение.Колонки.Удалить(СтрСоотвествие.ИмяКолонки);	
		Элементы.Удалить(Элементы.Найти("ТаблицаВремяНаФорме"+СтрСоотвествие.ИмяКолонки));
	КонецЦикла;
	
	СоответствиеПериодовНазванийКолонок.Очистить();
	
	Месяц		= НачалоМесяца(Объект.НачалоПериода);
	НомерМесяца	= 1;
	
	Пока Месяц <= Объект.КонецПериода Цикл
		
		ИмяКолонки	= "Месяц" + Формат(НомерМесяца,"ЧГ=");
		
		ТаблицаВремяНаФормеЗначение.Колонки.Добавить(ИмяКолонки,,Формат(Месяц,"ДФ='MMMM yyyy'"));
		
		НоваяКолонка = Новый РеквизитФормы(ИмяКолонки, Новый ОписаниеТипов("Число"), "ТЗ");
		
		Стр 			= СоответствиеПериодовНазванийКолонок.Добавить();
		Стр.ИмяКолонки 	= ИмяКолонки;
		Стр.Месяц 		= Месяц;
		
		Месяц		= НачалоМесяца(ДобавитьМесяц(Месяц,1));
		НомерМесяца	= НомерМесяца + 1;
		
	КонецЦикла;	
	
    МассивРеквизитов.Очистить();
	Для Каждого Колонка ИЗ ТаблицаВремяНаФормеЗначение.Колонки Цикл
		Если СтрНайти(Колонка.Имя,"Месяц") <> 0 Тогда
			НоваяКолонка = Новый РеквизитФормы(Колонка.Имя, Новый ОписаниеТипов("Число"), "ТаблицаВремяНаФорме");
			МассивРеквизитов.Добавить(НоваяКолонка);
		КонецЕсли;
    КонецЦикла;      
    ИзменитьРеквизиты(МассивРеквизитов);  
    ЗначениеВРеквизитФормы(ТаблицаВремяНаФормеЗначение, "ТаблицаВремяНаФорме");
	
    ЭлементТаблицаВремяНаФорме = Элементы.ТаблицаВремяНаФорме;
	Для Каждого Колонка ИЗ ТаблицаВремяНаФормеЗначение.Колонки Цикл
		Если СтрНайти(Колонка.Имя,"Месяц") <> 0 Тогда
			НовыйЭлементФормы 				= Элементы.Добавить("ТаблицаВремяНаФорме"+Колонка.Имя, Тип("ПолеФормы"), ЭлементТаблицаВремяНаФорме);
			НовыйЭлементФормы.Вид 			= ВидПоляФормы.ПолеВвода;
			НовыйЭлементФормы.ПутьКДанным 	= "ТаблицаВремяНаФорме." + Колонка.Имя;
			НовыйЭлементФормы.Заголовок		= Колонка.Заголовок;
		КонецЕсли;
    КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьТаблицуНаФормеИзОбъекта()
	
	Для Каждого Стр Из Объект.Время Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Месяц", Стр.Месяц);
		НайденныеСтроки = СоответствиеПериодовНазванийКолонок.НайтиСтроки(ПараметрыОтбора);
		
		Для Каждого НайдСтрока Из НайденныеСтроки Цикл
			НазваниеКолонки = НайдСтрока.ИмяКолонки;
		КонецЦикла;
		
		Если НайденныеСтроки.Количество() = 0 Тогда 
			Продолжить;
		КонецЕсли;		
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Должность", Стр.Должность);
		НайденныеСтроки = ТаблицаВремяНаФорме.НайтиСтроки(ПараметрыОтбора);
		
		Для Каждого НайдСтрока Из НайденныеСтроки Цикл
			НайдСтрока[НазваниеКолонки] = Стр.КоличествоЧасов;	
		КонецЦикла;
		
		Если НайденныеСтроки.Количество() = 0 Тогда 
			СтрНовая					= ТаблицаВремяНаФорме.Добавить();
			СтрНовая.Должность			= Стр.Должность;
			СтрНовая[НазваниеКолонки]	= Стр.КоличествоЧасов;
		КонецЕсли;		
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуВОбъекте()
	Объект.Время.Очистить();
	Для Каждого Колонка Из СоответствиеПериодовНазванийКолонок Цикл
		Для Каждого Стр Из ТаблицаВремяНаФорме Цикл
			СтрОбъекта 					= Объект.Время.Добавить();
			СтрОбъекта.Месяц 			= Колонка.Месяц;
			СтрОбъекта.Должность 		= Стр.Должность;
			СтрОбъекта.КоличествоЧасов	= Стр[Колонка.ИмяКолонки];
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ДобавитьПерсоналНаСервере(ВыбранноеЗначение)
	
	Если ВыбранноеЗначение.ЭтоГруппа Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Персонал.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Персонал КАК Персонал
		|ГДЕ
		|	Персонал.Ссылка В ИЕРАРХИИ(&Группа)
		|	И НЕ Персонал.ПометкаУдаления
		|	И НЕ Персонал.ЭтоГруппа";
		
		Запрос.УстановитьПараметр("Группа", ВыбранноеЗначение);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Должность", ВыборкаДетальныеЗаписи.Ссылка);
			НайдСтроки = ТаблицаВремяНаФорме.НайтиСтроки(ПараметрыОтбора);
			
			Если НайдСтроки.Количество() = 0 Тогда 
				НовСтр				= ТаблицаВремяНаФорме.Добавить();
				НовСтр.Должность	= ВыборкаДетальныеЗаписи.Ссылка;
			Иначе 
				Сообщить("Должность " + Строка(ВыборкаДетальныеЗаписи.Ссылка) + " уже добавлена.");
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Должность",ВыбранноеЗначение);
		НайдСтроки = ТаблицаВремяНаФорме.НайтиСтроки(ПараметрыОтбора);
		
		Если НайдСтроки.Количество() = 0 Тогда 
			НовСтр				= ТаблицаВремяНаФорме.Добавить();
			НовСтр.Должность	= ВыбранноеЗначение;
		Иначе 
			Сообщить("Должность " + Строка(ВыбранноеЗначение) + " уже добавлена.");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КоличествоЧасовИзПерсонала(Персонал)
	Возврат Персонал.ГрафикРаботы.КоличествоЧасовВДень
КонецФункции

#КонецОбласти
