#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриИзмененииМесяца();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для каждого СтрокаТабеля Из Объект.Табель Цикл
		ПересчитатьИтогСтрокиТабеля(СтрокаТабеля);
	КонецЦикла; 
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ИсточникВыбора) = Тип("ФормаКлиентскогоПриложения")
		И СтрНайти(ИсточникВыбора.ИмяФормы, "ФормаКалендаря") > 0 Тогда
		
		Объект.Месяц = ВыбранноеЗначение;
		ПриИзмененииМесяца();
				
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура ТабельТранспортПриИзменении(Элемент)
	  ТабельТранспортПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтображениеМесяцаРегулирование(Элемент, Направление, СтандартнаяОбработка)
		
	Объект.Месяц = ?(ЗначениеЗаполнено(Объект.Месяц), 
							ДобавитьМесяц(Объект.Месяц, Направление),
							ДобавитьМесяц(НачалоМесяца(ОбщегоНазначенияКлиент.ДатаСеанса()), Направление));
	
	ПриИзмененииМесяца();
							
КонецПроцедуры

&НаКлиенте
Процедура ОтображениеМесяцаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.Месяц) Тогда
		ДатаКалендаряПриОткрытии = Объект.Месяц;
	Иначе
		ДатаКалендаряПриОткрытии = ОбщегоНазначенияКлиент.ДатаСеанса();
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ФормаКалендаря", ОбщегоНазначенияКЛАДОКлиент.ПараметрыОткрытияФормыКалендаря(
		ДатаКалендаряПриОткрытии), ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТабель

&НаКлиенте
Процедура ТабельЧасыПриИзменении(Элемент)
	ТабельЧасыПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьТабель(Команда)	
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли; 
	
	ЗаполнитьТабельНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ТабельТранспортПриИзмененииНаСервере()
	
	ИдТекущейСтроки = Элементы.Табель.ТекущаяСтрока;
	
	ТекущаяСтрокаТебель = Объект.Табель.НайтиПоИдентификатору(ИдТекущейСтроки);
	ТекущаяСтрокаТебель.Подрядчик = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокаТебель.Транспорт, "Предприятие");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабельНаСервере()
	
	Объект.Табель.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕжедневноеИспользованиеРесурсовИнструменты.Инструмент КАК Транспорт,
	|	ЕжедневноеИспользованиеРесурсовИнструменты.Инструмент.Предприятие КАК Подрядчик,
	|	ДЕНЬ(ЕжедневноеИспользованиеРесурсовИнструменты.Ссылка.Дата) КАК День,
	|	СУММА(ЕжедневноеИспользованиеРесурсовИнструменты.Количество) КАК Часы
	|ПОМЕСТИТЬ ВТ_Результат
	|ИЗ
	|	Документ.ЕжедневноеИспользованиеРесурсов.Инструменты КАК ЕжедневноеИспользованиеРесурсовИнструменты
	|ГДЕ
	|	ЕжедневноеИспользованиеРесурсовИнструменты.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И ЕжедневноеИспользованиеРесурсовИнструменты.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ЕжедневноеИспользованиеРесурсовИнструменты.Инструмент.Подразделение = &Подразделение
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕжедневноеИспользованиеРесурсовИнструменты.Инструмент,
	|	ЕжедневноеИспользованиеРесурсовИнструменты.Инструмент.Предприятие,
	|	ДЕНЬ(ЕжедневноеИспользованиеРесурсовИнструменты.Ссылка.Дата)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Результат.Транспорт КАК Транспорт,
	|	ВТ_Результат.Подрядчик КАК Подрядчик,
	|	ВТ_Результат.День КАК День,
	|	ВТ_Результат.Часы КАК Часы
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|ИТОГИ ПО
	|	Транспорт";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.Месяц));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Объект.Месяц)); 
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение); 
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Транспорт");
	
	Пока РезультатЗапроса.Следующий() Цикл
		
		НоваяСтрокаТабеля = Объект.Табель.Добавить();		
		НоваяСтрокаТабеля.Транспорт = РезультатЗапроса.Транспорт;

		ВыборкаПодчиненная = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаПодчиненная.Следующий() Цикл
			НоваяСтрокаТабеля["Часы" + ВыборкаПодчиненная.День] = ВыборкаПодчиненная.Часы;
			НоваяСтрокаТабеля.ИтогЧасы = НоваяСтрокаТабеля.ИтогЧасы + ВыборкаПодчиненная.Часы;
			НоваяСтрокаТабеля.Подрядчик = ВыборкаПодчиненная.Подрядчик;
		КонецЦикла;
		
	КонецЦикла;  
	
КонецПроцедуры

&НаСервере
Процедура ТабельЧасыПриИзмененииНаСервере()
	ТекущаяСтрокаТабель = Объект.Табель.НайтиПоИдентификатору(Элементы.Табель.ТекущаяСтрока);
	
	ПересчитатьИтогСтрокиТабеля(ТекущаяСтрокаТабель);
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИтогСтрокиТабеля(СтрокаТабеля)
		
	СтрокаТабеля.ИтогЧасы = Неопределено;
	СтрокаТабеля.ИтогРабочиеДни = Неопределено;
	
	Для Счетчик = 1 По 31 Цикл
		
		СтрокаТабеля.ИтогЧасы = СтрокаТабеля.ИтогЧасы + СтрокаТабеля["Часы" + Счетчик];
		СтрокаТабеля.ИтогРабочиеДни = СтрокаТабеля.ИтогРабочиеДни + ЗначениеЗаполнено(СтрокаТабеля["Часы" + Счетчик]);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииМесяца()
	
	Объект.Месяц = НачалоМесяца(Объект.Месяц);
	ОтображениеМесяца = Формат(Объект.Месяц, НСтр("ru = 'ДФ=''MMMM yyyy'''"));
	
	Для День = 29 По День(КонецМесяца(Объект.Месяц)) Цикл
		Элементы["ТабельЧасы" + День].Видимость = Истина;
	КонецЦикла;
	
	Для День = День(КонецМесяца(Объект.Месяц)) + 1 По 31 Цикл
		
		Элементы["ТабельЧасы" + День].Видимость = Ложь;
		
		Для каждого СтрокаТаблицы Из Объект.Табель Цикл
			
			СтрокаТаблицы["Часы" + День] = Неопределено;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 
 
