#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ТипВыбранногоЗначения = ТипЗнч(ВыбранноеЗначение);
	
	Если ТипВыбранногоЗначения = Тип("СправочникСсылка.Персонал") Тогда
		
		НоваяСтрокаПерсонал = Объект.Персонал.Добавить();
		НоваяСтрокаПерсонал.Персонал = ВыбранноеЗначение;
		ПриИзмененииПерсонала(НоваяСтрокаПерсонал);
		
	ИначеЕсли ТипВыбранногоЗначения = Тип("СправочникСсылка.Инструменты") Тогда 
		
		НоваяСтрокаИнструмент = Объект.Инструменты.Добавить();
		НоваяСтрокаИнструмент.Инструмент = ВыбранноеЗначение;
		ИнструментПриИзмененииНаСервере(НоваяСтрокаИнструмент.ПолучитьИдентификатор());
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИнструменты

&НаКлиенте
Процедура ИнструментыИнструментПриИзменении(Элемент)
	
	ИнструментПриИзмененииНаСервере(Элементы.Инструменты.ТекущаяСтрока);
		
КонецПроцедуры

&НаКлиенте
Процедура ИнструментыКоличествоПриИзменении(Элемент)
	
	Элементы.Инструменты.ТекущиеДанные.Сумма = Элементы.Инструменты.ТекущиеДанные.Цена * Элементы.Инструменты.ТекущиеДанные.Количество;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнструментыДоговорПриИзменении(Элемент)
	
	Элементы.Инструменты.ТекущиеДанные.Продукт = ПолучитьПродукт(Элементы.Инструменты.ТекущиеДанные.Договор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПерсонал

&НаКлиенте
Процедура ПерсоналПерсоналПриИзменении(Элемент)
	
	ПриИзмененииПерсонала(Элементы.Персонал.ТекущиеДанные);
		
КонецПроцедуры

&НаКлиенте
Процедура ПерсоналКоличествоПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Элементы.Персонал.ТекущиеДанные) Тогда 
		Элементы.Персонал.ТекущиеДанные.Сумма = Элементы.Персонал.ТекущиеДанные.Цена * Элементы.Персонал.ТекущиеДанные.Количество;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПерсоналДоговорПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Элементы.Персонал.ТекущиеДанные) Тогда 
		Элементы.Персонал.ТекущиеДанные.Продукт = ПолучитьПродукт(Элементы.Персонал.ТекущиеДанные.Договор);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалы

&НаКлиенте
Процедура МатериалыКоличествоПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Элементы.Материалы.ТекущиеДанные) Тогда 
		Элементы.Материалы.ТекущиеДанные.Сумма = Элементы.Материалы.ТекущиеДанные.Цена * Элементы.Материалы.ТекущиеДанные.Количество;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура МатериалыДоговорПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Элементы.Материалы.ТекущиеДанные) Тогда 
		Элементы.Материалы.ТекущиеДанные.Продукт = ПолучитьПродукт(Элементы.Материалы.ТекущиеДанные.Договор);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборСоСклада(Команда)
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодбораСоСклада", ЭтотОбъект);
	
	ОткрытьФорму("Документ.ЕжедневноеИспользованиеРесурсов.Форма.ПодборСоСклада", , 
		ЭтотОбъект, УникальныйИдентификатор, , , ОписаниеОповещения);  
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЦеныМатериалы(Команда)
	ЗаполнитьЦеныМатериалыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЦеныПерсонал(Команда)
	ЗаполнитьЦеныПерсоналНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЦеныИнструменты(Команда)
	ЗаполнитьЦеныИнструментыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПодборПерсонал(Команда)
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ЗакрыватьПриВыборе", Ложь);
			
	ОткрытьФорму("Справочник.Персонал.Форма.ФормаВыбора", ПараметрыПодбора, ЭтотОбъект, УникальныйИдентификатор);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборТранспорт(Команда)
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.Инструменты.ФормаВыбора", ПараметрыПодбора, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриИзмененииПерсонала(ТекущаяСтрокаПерсонал)
	
	ТекущаяСтрокаПерсонал.СтатьяЗатрат = СтатьяЗатратИзСправочника(ТекущаяСтрокаПерсонал.Персонал);
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьЦеныПерсоналНаСервере()
	
	Для Каждого СтрокаПерсонал Из Объект.Персонал Цикл
		СтрокаПерсонал.Цена = РегистрыСведений.ФактическиеЦеныНаРесурсы.ПолучитьЦенуРесурса(
			Объект.Дата, Объект.Организация, СтрокаПерсонал.Персонал, СтрокаПерсонал.СтатьяЗатрат, Объект.Сценарий);
		
		СтрокаПерсонал.Сумма = СтрокаПерсонал.Количество * СтрокаПерсонал.Цена;
	КонецЦикла;
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьЦеныМатериалыНаСервере()
	
	Для каждого СтрокаМатериал Из Объект.Материалы Цикл
		СтрокаМатериал.Цена = РегистрыСведений.ФактическиеЦеныНаРесурсы.ПолучитьЦенуРесурса(
			Объект.Дата, Объект.Организация, СтрокаМатериал.Материал, СтрокаМатериал.СтатьяЗатрат, Объект.Сценарий);
		
		СтрокаМатериал.Сумма = СтрокаМатериал.Количество * СтрокаМатериал.Цена;		
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЦеныИнструментыНаСервере()
	
	Для каждого СтрокаИнструмент Из Объект.Инструменты Цикл
		СтрокаИнструмент.Цена = РегистрыСведений.ФактическиеЦеныНаРесурсы.ПолучитьЦенуРесурса(
			Объект.Дата, Объект.Организация, СтрокаИнструмент.Инструмент, СтрокаИнструмент.СтатьяЗатрат, Объект.Сценарий);
		
		СтрокаИнструмент.Сумма = СтрокаИнструмент.Количество * СтрокаИнструмент.Цена;		
	КонецЦикла; 
	
КонецПроцедуры
 
&НаСервереБезКонтекста
Функция СтатьяЗатратИзСправочника(ЭлементСправочника)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементСправочника, "СтатьяЗатрат", Истина);
	
КонецФункции

&НаСервере
Процедура ИнструментПриИзмененииНаСервере(НомерТекущейСтрокиИнструмент)
	
	ТекущаяСтрокаТаблицы = Объект.Инструменты.НайтиПоИдентификатору(НомерТекущейСтрокиИнструмент);
	
	Если ТекущаяСтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	РеквизитыИнструмента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущаяСтрокаТаблицы.Инструмент, "СтатьяЗатрат, НаименованиеФакт", Истина);
	
	ТекущаяСтрокаТаблицы.СтатьяЗатрат     = РеквизитыИнструмента.СтатьяЗатрат;
	ТекущаяСтрокаТаблицы.НаименованиеФакт = РеквизитыИнструмента.НаименованиеФакт;

КонецПроцедуры
 
&НаСервереБезКонтекста
Функция ПолучитьПродукт(Договор) 
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор,"Проект.Продукт");
КонецФункции

&НаКлиенте
Процедура ПослеПодбораСоСклада(АдресВыбранныхМатериалов, ДополнительныеПараметры) Экспорт
	
	Если АдресВыбранныхМатериалов = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ПослеПодбораСоСкладаНаСервере(АдресВыбранныхМатериалов);
	
КонецПроцедуры

&НаСервере
Процедура ПослеПодбораСоСкладаНаСервере(АдресВыбранныхМатериалов)
	
	ВыбранныеМатериалы = ПолучитьИзВременногоХранилища(АдресВыбранныхМатериалов);
	СтруктураПоискаСтрок = Новый Структура;
	СтруктураПоискаСтрок.Вставить("Материал");
	
	Для каждого СтрокаВыбранныйМатериал Из ВыбранныеМатериалы Цикл
		
		СтруктураПоискаСтрок.Материал = СтрокаВыбранныйМатериал.Материал;
		НайденныеСтроки = Объект.Материалы.НайтиСтроки(СтруктураПоискаСтрок);
		
		Если НайденныеСтроки.Количество() Тогда
			Продолжить;
		КонецЕсли; 
		
		НоваяСтрокаМатериалы = Объект.Материалы.Добавить();
		НоваяСтрокаМатериалы.Материал   = СтрокаВыбранныйМатериал.Материал;
		НоваяСтрокаМатериалы.Сумма      = СтрокаВыбранныйМатериал.Сумма;
		НоваяСтрокаМатериалы.Количество = СтрокаВыбранныйМатериал.Количество;
		НоваяСтрокаМатериалы.Цена       = СтрокаВыбранныйМатериал.Цена;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	  ПересчитатьСтрокуТЧУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСтрокуТЧУслуги()
	
	ТекущиеДанныеУслуги = Элементы.Услуги.ТекущиеДанные;
	
	Если ТекущиеДанныеУслуги = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ТекущиеДанныеУслуги.Сумма = ТекущиеДанныеУслуги.Цена * ТекущиеДанныеУслуги.Количество;
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	ПересчитатьСтрокуТЧУслуги();
КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаПриИзменении(Элемент)
	ПересчитатьСтрокуТЧУслуги();
КонецПроцедуры
  
#КонецОбласти 

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
     ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

