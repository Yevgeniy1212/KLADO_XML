Процедура ЗаписатьИзмененныеРеквизиты(ЭтотОбъект, НачальноеЗаполнение = Ложь, АвторИзменения = Неопределено) Экспорт
	Если АвторИзменения = Неопределено Тогда
		ТекущийПользователь = Пользователи.ТекущийПользователь();
	Иначе
		ТекущийПользователь = АвторИзменения;
	КонецЕсли;
	//Реквизиты = ЭтотОбъект.Метаданные().Реквизиты;
	
	Если НачальноеЗаполнение Тогда
		ОбъектТек = ПолучитьСтруктуруРеквизитов(ЭтотОбъект);
		ЗаполнитьЗначенияСвойств(ОбъектТек, ЭтотОбъект);
		Для Каждого Реквизит ИЗ ОбъектТек Цикл
			МЗ = РегистрыСведений.ИсторияИзменения.СоздатьМенеджерЗаписи();
			МЗ.Объект = ЭтотОбъект.Ссылка;
			МЗ.Пользователь = ТекущийПользователь;
			МЗ.Период = ТекущаяДатаСеанса();
			МЗ.НазваниеРеквизита = Реквизит.Ключ;
			МЗ.Значение = Реквизит.Значение;
			МЗ.Причина = "начальное заполнение";
			
			МЗ.Записать(Истина);
		КонецЦикла;
	Иначе
		ОбъектПред = ПолучитьПоследниеИзмененияРеквизитов(ЭтотОбъект, ЭтотОбъект.Ссылка);
		Для Каждого Реквизит ИЗ ОбъектПред Цикл
			Если ЭтотОбъект[Реквизит.Ключ] <> ОбъектПред[Реквизит.Ключ] Тогда
				МЗ = РегистрыСведений.ИсторияИзменения.СоздатьМенеджерЗаписи();
				МЗ.Объект = ЭтотОбъект.Ссылка;
				МЗ.Пользователь = ТекущийПользователь;
				МЗ.Период = ТекущаяДатаСеанса();
				МЗ.НазваниеРеквизита = Реквизит.Ключ;
				МЗ.Значение = ЭтотОбъект[Реквизит.Ключ];
				//МЗ.Причина = "служебное изменение";
				
				МЗ.Записать(Истина);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Функция ЕстьЗаписиВРегистреИзменения(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИсторияИзмененияСрезПоследних.Период КАК Период
	               |ИЗ
	               |	РегистрСведений.ИсторияИзменения.СрезПоследних(
	               |			,
	               |			Объект = &Объект
	               |				И НЕ НазваниеРеквизита В (&СписокНепроверяемыхРеквизитов)) КАК ИсторияИзмененияСрезПоследних";
	Запрос.УстановитьПараметр("СписокНепроверяемыхРеквизитов", ПолучитьСписокНепроверяемыхРеквизитов());
	Запрос.УстановитьПараметр("Объект", Ссылка);
	Результат = Запрос.Выполнить();	
	Возврат НЕ Результат.Пустой();	
КонецФункции

Функция ПолучитьПоследниеИзмененияРеквизитов(ТекОбъект, Ссылка)  Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИсторияИзмененияСрезПоследних.Период КАК Период,
	               |	ИсторияИзмененияСрезПоследних.НазваниеРеквизита КАК НазваниеРеквизита,
	               |	ИсторияИзмененияСрезПоследних.Значение КАК Значение
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	РегистрСведений.ИсторияИзменения.СрезПоследних(
	               |			,
	               |			Объект = &Объект
	               |				И НЕ НазваниеРеквизита В (&СписокНепроверяемыхРеквизитов)) КАК ИсторияИзмененияСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(ВТ.Период) КАК Период,
	               |	ВТ.НазваниеРеквизита КАК НазваниеРеквизита
	               |ПОМЕСТИТЬ ВТ2
	               |ИЗ
	               |	ВТ КАК ВТ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ.НазваниеРеквизита
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.Период КАК Период,
	               |	ВТ.НазваниеРеквизита КАК НазваниеРеквизита,
	               |	ВТ.Значение КАК Значение
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ2 КАК ВТ2
	               |		ПО ВТ.Период = ВТ2.Период
	               |			И ВТ.НазваниеРеквизита = ВТ2.НазваниеРеквизита";
	Запрос.УстановитьПараметр("Объект", Ссылка);
	Запрос.УстановитьПараметр("СписокНепроверяемыхРеквизитов", ПолучитьСписокНепроверяемыхРеквизитов());
	Выборка = Запрос.Выполнить().Выбрать();
	СтруктураРекв = ПолучитьСтруктуруРеквизитов(ТекОбъект);
	Пока Выборка.Следующий() Цикл
		Если СтруктураРекв.Свойство(Выборка.НазваниеРеквизита) Тогда
			СтруктураРекв.Вставить(Выборка.НазваниеРеквизита, Выборка.Значение);
		КонецЕсли;
	КонецЦикла;
	Возврат СтруктураРекв;	
КонецФункции

Процедура ЗаписатьИзмененияРеквизитов(Ссылка, ИзмененияРеквизитов) Экспорт
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Для Каждого Строка ИЗ ИзмененияРеквизитов Цикл
		МЗ = РегистрыСведений.ИсторияИзменения.СоздатьМенеджерЗаписи();
		МЗ.Объект = Ссылка;
		МЗ.Пользователь = ТекущийПользователь;
		МЗ.Период = ТекущаяДатаСеанса();
		ЗаполнитьЗначенияСвойств(МЗ, Строка);
		МЗ.Записать();
	КонецЦикла;
КонецПроцедуры

Процедура ПолучитьИзмененныеРеквизиты(ТекОбъект, Объект, ИзмененияРеквизитов)  Экспорт
	ОбъектПред = ИзменениеРеквизитовСервер.ПолучитьПоследниеИзмененияРеквизитов(ТекОбъект, Объект.Ссылка);
	Для Каждого Реквизит ИЗ ОбъектПред Цикл
		СтрокаИзм = ИзмененияРеквизитов.НайтиСтроки(Новый Структура("НазваниеРеквизита", Реквизит.Ключ));
		Если СтрокаИзм.Количество() > 0 Тогда
			Стр = СтрокаИзм[0];
			Если Объект[Реквизит.Ключ] <> ОбъектПред[Реквизит.Ключ] Тогда
				Стр.Значение = Объект[Реквизит.Ключ];
			Иначе
				ИзмененияРеквизитов.Удалить(Стр);
			КонецЕсли;
		Иначе
			Если Объект[Реквизит.Ключ] <> ОбъектПред[Реквизит.Ключ] Тогда
				Стр = ИзмененияРеквизитов.Добавить();
				Стр.НазваниеРеквизита = Реквизит.Ключ;
				Стр.Значение = Объект[Реквизит.Ключ];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


Функция ПолучитьСтруктуруРеквизитов(ТекОбъект)
	
	СписокНепроверяемыхРеквизитов = ПолучитьСписокНепроверяемыхРеквизитов();
	СтруктураРекв = Новый Структура;
	Реквизиты = ТекОбъект.Метаданные().Реквизиты;
	СтандартныеРеквизиты = ТекОбъект.Метаданные().СтандартныеРеквизиты;
	Для Каждого Реквизит Из Реквизиты Цикл
		Если СписокНепроверяемыхРеквизитов.Найти(Реквизит.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтруктураРекв.Вставить(Реквизит.Имя, Неопределено);	
	КонецЦикла;
	Для Каждого Реквизит Из СтандартныеРеквизиты Цикл
		Если СписокНепроверяемыхРеквизитов.Найти(Реквизит.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтруктураРекв.Вставить(Реквизит.Имя, Неопределено);	
	КонецЦикла;
	Возврат СтруктураРекв;
КонецФункции

Функция ПолучитьСписокНепроверяемыхРеквизитов()
	Массив = Новый Массив;
	Массив.Добавить("ИмяПредопределенныхДанных");
	Массив.Добавить("Предопределенный");
	Массив.Добавить("Ссылка");
	Возврат Массив;
КонецФункции