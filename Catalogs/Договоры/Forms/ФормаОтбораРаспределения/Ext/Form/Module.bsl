
&НаСервере
Процедура ЗаполнитьПоОтборуНаСервере()
	Если НЕ ВариантРаспределения = 4 Тогда
		Если ВариантРаспределения = 0 Тогда
			ИмяКолонки = "СуммаПлан";
		ИначеЕсли ВариантРаспределения = 1 Тогда
			ИмяКолонки = "СуммаНачисления";
		ИначеЕсли ВариантРаспределения = 2 Тогда
			ИмяКолонки = "СуммаПоДоговору";
		КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Договоры.Ссылка КАК Договор,
		|	0 КАК Коэффициент,
		|	Договоры.СуммаПлан КАК Сумма
		|ИЗ
		|	Справочник.Договоры КАК Договоры
		|ГДЕ
		|	Договоры.ТипДоговора = ЗНАЧЕНИЕ(перечисление.типыДоговоров.дт1)
		|	И НЕ Договоры.ПометкаУдаления
		|	//Отбор СтатусДоговора//
		|	//Отбор Подразделение//
		|	//Отбор Период//
		|	//Отбор Продукт//
		|	//Отбор Проект//";
		Если ВариантРаспределения = 1 Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Договоры.СуммаПлан КАК Сумма", "Договоры.СуммаНачисления КАК Сумма" + Символы.ПС);
		ИначеЕсли ВариантРаспределения = 2 Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Договоры.СуммаПлан КАК Сумма", "Договоры.СуммаПоДоговору КАК Сумма" + Символы.ПС);
		КонецЕсли;
		Если Продукт.Количество() > 0 Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//Отбор Продукт//", "И Договоры.Проект.Продукт В (&Продукт)" + Символы.ПС);
			Запрос.УстановитьПараметр("Продукт", Продукт);
		КонецЕсли;
		Если Подразделение.Количество() > 0 Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//Отбор Подразделение//", "И Договоры.Подразделение В (&Подразделение)" + Символы.ПС);
			Запрос.УстановитьПараметр("Подразделение", Подразделение);
		КонецЕсли;
		Если Проект.Количество() > 0 Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//Отбор Проект//", "И Договоры.Проект В (&Проект)" + Символы.ПС);
			Запрос.УстановитьПараметр("Проект", Проект);
		КонецЕсли;
		Если ЗначениеЗаполнено(Период) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//Отбор Период//", "И Договоры.ДатаНачала <= &ДатаОкончания	И Договоры.ДатаОкончания >= &ДатаНачала" + Символы.ПС);
			Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
			Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
		КонецЕсли;
		Если ЗначениеЗаполнено(СтатусДоговора) Тогда 
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//Отбор СтатусДоговора//", "И Договоры.СостояниеИсполнения В (&СостояниеИсполнения)" + Символы.ПС);
			Запрос.УстановитьПараметр("СостояниеИсполнения", СтатусДоговора);
		КонецЕсли;
		ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(), "СписокДоговоров");
	Иначе
		СписокДоговоров.Очистить();
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Договоры.Ссылка КАК Договор,
		|	Договоры.Проект КАК Проект,
		|	Договоры.СуммаНачисления КАК СуммаНачисления,
		|	Договоры.ТипДоговора КАК ТипДоговора
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Справочник.Договоры КАК Договоры
		|ГДЕ
		|	Договоры.Проект.Продукт В (&Продукт)
		|	И НЕ Договоры.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Проект КАК Проект,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ.Договор) КАК КолДоговор
		|ПОМЕСТИТЬ ВТ_КоличествоДТВПроекте
		|ИЗ
		|	ВТ КАК ВТ
		|ГДЕ
		|	НЕ ВТ.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.ДТ1)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.Проект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Проект КАК Проект,
		|	СУММА(ВТ.СуммаНачисления) КАК СуммаНачисления
		|ПОМЕСТИТЬ ВТ_СуммыПроект
		|ИЗ
		|	ВТ КАК ВТ
		|ГДЕ
		|	ВТ.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.ДТ1)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.Проект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Договор КАК Договор,
		|	ВТ.Проект КАК Проект,
		|	ВТ.СуммаНачисления КАК СуммаНачисления,
		|	ЕСТЬNULL(ВТ_СуммыПроект.СуммаНачисления, 0) КАК СуммаНачисленияПоПроекту,
		|	ЕСТЬNULL(ВТ_КоличествоДТВПроекте.КолДоговор, 0) КАК КолДоговорПроект,
		|	ВложенныйЗапрос.КолДоговор КАК КолДоговорОбщее
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыПроект КАК ВТ_СуммыПроект
		|		ПО ВТ.Проект = ВТ_СуммыПроект.Проект
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоДТВПроекте КАК ВТ_КоличествоДТВПроекте
		|		ПО ВТ.Проект = ВТ_КоличествоДТВПроекте.Проект
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СУММА(ВТ_КоличествоДТВПроекте.КолДоговор) КАК КолДоговор
		|		ИЗ
		|			ВТ_КоличествоДТВПроекте КАК ВТ_КоличествоДТВПроекте) КАК ВложенныйЗапрос
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВТ.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.ДТ1)";
		Запрос.УстановитьПараметр("Продукт", Продукт);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Строка = СписокДоговоров.Добавить();
			Строка.Договор = Выборка.Договор;
			Если Выборка.КолДоговорОбщее = 0 ИЛИ  Выборка.СуммаНачисленияПоПроекту = 0 Тогда
				Продолжить;
			КонецЕсли;
			Строка.Коэффициент = Выборка.КолДоговорПроект / Выборка.КолДоговорОбщее * Выборка.СуммаНачисления / Выборка.СуммаНачисленияПоПроекту;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОтбору(Команда)
	Если ВариантРаспределения = 4
		И Продукт.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для текущего распределения необходимо заполнить продукт.");
		Возврат;
	КонецЕсли;
	ЗаполнитьПоОтборуНаСервере();
КонецПроцедуры

&НаСервере
Функция СформироватьТЗ()
	Если ВариантРаспределения = 4 Тогда
		ТЗ = СписокДоговоров.Выгрузить(, "Договор,Коэффициент");
	Иначе
		ТЗ = СписокДоговоров.Выгрузить(, "Договор,Сумма");
	КонецЕсли;
	Возврат ПоместитьВоВременноеХранилище(ТЗ);
КонецФункции

&НаКлиенте
Процедура Перенести(Команда)
	Закрыть(Новый Структура("ТЗ, ВариантРаспределения, РаспределятьОстаток", СформироватьТЗ(), ВариантРаспределения, РаспределятьОстаток));
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВариантРаспределения = 1;
КонецПроцедуры

&НаКлиенте
Процедура ВариантРаспределенияПриИзменении(Элемент)
	Элементы.СписокДоговоровКоэффициент.Видимость = ВариантРаспределения = 4;
	Элементы.СписокДоговоровСумма.Видимость = НЕ ВариантРаспределения = 4;
КонецПроцедуры
