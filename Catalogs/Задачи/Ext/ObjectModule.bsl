
Процедура ПриЗаписи(Отказ)
	
	Если НЕ Отказ  Тогда
		//меняем дату окончания родительских задач
		
		Если ЗначениеЗаполнено(Родитель) 
			И Родитель.ДатаОкончания < ДатаОкончания Тогда
			ЗадачаОб = Родитель.ПолучитьОбъект();
			ЗадачаОб.ДатаОкончания = ДатаОкончания;
			//ЗадачаОб.ДополнительныеСвойства.Вставить("ИзменениеДатыОкончания", Истина);
			ЗадачаОб.Записать();
		КонецЕсли;
	КонецЕсли;
	
	// Начало Изменение реквизитов
	Если Константы.ИспользоватьЛогирование.Получить()  Тогда
		Если ДополнительныеСвойства.Свойство("АвторИзменения") Тогда
			АвторИзменения = ДополнительныеСвойства.АвторИзменения;
		Иначе
			АвторИзменения = Пользователи.ТекущийПользователь();
		КонецЕсли;
		ЕстьЗаписиВРегистреИзменения = ИзменениеРеквизитовСервер.ЕстьЗаписиВРегистреИзменения(Ссылка);
		Если Не ЕстьЗаписиВРегистреИзменения Тогда
			ИзменениеРеквизитовСервер.ЗаписатьИзмененныеРеквизиты(ЭтотОбъект, Истина, АвторИзменения);
		КонецЕсли;
		Если НЕ (ДополнительныеСвойства.Свойство("ИзмененияОбработаны")
			И ДополнительныеСвойства.ИзмененияОбработаны) 
			И ЕстьЗаписиВРегистреИзменения Тогда
			ИзменениеРеквизитовСервер.ЗаписатьИзмененныеРеквизиты(ЭтотОбъект,,АвторИзменения);
		КонецЕсли;
	КонецЕсли;
	// Конец Изменение реквизитов
	
	// Начало Изменение даты окончания
	Если НЕ Константы.ИспользоватьЛогирование.Получить()  Тогда
		Если ДополнительныеСвойства.Свойство("АвторИзменения") Тогда
			АвторИзменения = ДополнительныеСвойства.АвторИзменения;
		Иначе
			АвторИзменения = Пользователи.ТекущийПользователь();
		КонецЕсли;
		ЗаписатьИзмененныеРеквизиты(ЭтотОбъект, ДатаОкончания, АвторИзменения);
	КонецЕсли;
	// Конец Изменение даты окончания
	
	Если ДополнительныеСвойства.Свойство("НоваяСторонаЗадачи") Тогда
		//поменять сторону у всех подчиненных задач
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.Текст = "ВЫБРАТЬ
		               |	Задачи.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.Задачи КАК Задачи
		               |ГДЕ
		               |	Задачи.Ссылка В ИЕРАРХИИ(&Ссылка)
					   |	И НЕ Задачи.Ссылка = &Ссылка";	
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗадачаОб = Выборка.Ссылка.ПолучитьОбъект();
			ЗадачаОб.СторонаЗадачи = ДополнительныеСвойства.НоваяСторонаЗадачи;
			ЗадачаОб.ДополнительныеСвойства.Вставить("ИзменениеСтороныЗадачи", Истина);
			ЗадачаОб.Записать();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если НЕ ДополнительныеСвойства.Свойство("ИзменениеСтороныЗадачи") 
		и Ссылка.СторонаЗадачи <> СторонаЗадачи Тогда
		ДополнительныеСвойства.Вставить("НоваяСторонаЗадачи", СторонаЗадачи);
	КонецЕсли;
	
	Если Ссылка.ПометкаУдаления <> ПометкаУдаления Тогда
		Если НЕ Пользователи.ЭтоПолноправныйПользователь()
			И Пользователи.ТекущийПользователь().Сотрудник <> Владелец.Администратор Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Устанавливать или снимать пометку удаления может только администратор договора.");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьИзмененныеРеквизиты(ЭтотОбъект, ДатаОкончания, АвторИзменения) Экспорт
	ПоследняяДата = ПолучитьПоследниеИзмененияРеквизитовДатаОкончания(Ссылка);
	
	Если ПоследняяДата = Неопределено Тогда
		МЗ = РегистрыСведений.ИсторияИзменения.СоздатьМенеджерЗаписи();
		МЗ.Объект = ЭтотОбъект.Ссылка;
		МЗ.Пользователь = АвторИзменения;
		МЗ.Период = ТекущаяДатаСеанса();
		МЗ.НазваниеРеквизита = "ДатаОкончания";
		МЗ.Значение = ДатаОкончания;
		МЗ.Причина = "начальное заполнение";
		
		МЗ.Записать(Истина);
	Иначе
		
		Если ПоследняяДата <> ДатаОкончания Тогда 
			МЗ = РегистрыСведений.ИсторияИзменения.СоздатьМенеджерЗаписи();
			МЗ.Объект = ЭтотОбъект.Ссылка;
			МЗ.Пользователь = АвторИзменения;
			МЗ.Период = ТекущаяДатаСеанса();
			МЗ.НазваниеРеквизита = "ДатаОкончания";
			МЗ.Значение = ДатаОкончания;
			МЗ.Причина = "изменение пользователем";
			
			МЗ.Записать(Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьПоследниеИзмененияРеквизитовДатаОкончания(Ссылка)  Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИсторияИзмененияСрезПоследних.Значение КАК Значение
	               |ИЗ
	               |	РегистрСведений.ИсторияИзменения.СрезПоследних(
	               |			,
	               |			Объект = &Объект
	               |				И НазваниеРеквизита = ""ДатаОкончания"") КАК ИсторияИзмененияСрезПоследних";
	Запрос.УстановитьПараметр("Объект", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Значение;	
	Иначе 
		Возврат Неопределено
	КонецЕсли;
КонецФункции

Процедура ПриКопировании(ОбъектКопирования)
	ПринятоКИсполнению = Ложь;
КонецПроцедуры
