
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
    
    СкриптHTML = ОбработкаОбъект.ПолучитьМакет("Макет").ПолучитьТекст();
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ 
    |   Договоры.Проект КАК Проект,
    |   Договоры.Ссылка КАК Ссылка,
    |   Договоры.Наименование КАК Наименование
    |ПОМЕСТИТЬ ВТ
    |ИЗ
    |   Справочник.Договоры КАК Договоры
    |;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ
    |   ВТ.Проект КАК Проект,
    |   ВТ.Ссылка КАК Ссылка,
    |   ВТ.Наименование КАК Наименование
    |ИЗ
    |   ВТ КАК ВТ
    |;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ РАЗЛИЧНЫЕ
    |   ВТ.Проект КАК Проект,
    |   ПРЕДСТАВЛЕНИЕ(ВТ.Проект) КАК ИмяПроекта
    |ИЗ
    |   ВТ КАК ВТ";
    ПакетЗапроса = Запрос.ВыполнитьПакет();
    РезультатЗапроса = ПакетЗапроса[1].Выбрать();
    Регионы = ПакетЗапроса[2].Выбрать(); 
    
    СтруктураСкрипта = Новый Структура("Data, Regions");
    
    МассивСтруктур = Новый Массив();
    
    Пока РезультатЗапроса.Следующий() Цикл
        
        СтруктураДанных = Новый Структура("Name, ID, Child, Region");
        
        СтруктураДанных.Name = РезультатЗапроса.Наименование;
        СтруктураДанных.ID = XMLСтрока(РезультатЗапроса.Ссылка);
        СтруктураДанных.Child = Неопределено;
        СтруктураДанных.Region = XMLСтрока(РезультатЗапроса.Проект);
        
        МассивСтруктур.Добавить(СтруктураДанных);
        
    КонецЦикла;
    
    МассивРегионов = Новый Массив;
    
    Пока Регионы.Следующий() Цикл
        
        СтруктураРегиона = Новый Структура("Name, ID");
        СтруктураРегиона.Name = XMLСтрока(Регионы.ИмяПроекта);
        СтруктураРегиона.ID = XMLСтрока(Регионы.Проект); 
        
        МассивРегионов.Добавить(СтруктураРегиона);
        
    КонецЦикла;
    
    СтруктураСкрипта.Data = МассивСтруктур;
    СтруктураСкрипта.Regions = МассивРегионов;
    
    ЗаписьРезультатаЗапроса = Новый ЗаписьJSON;
    ЗаписьРезультатаЗапроса.УстановитьСтроку();
    
    ЗаписатьJSON(ЗаписьРезультатаЗапроса, СтруктураСкрипта);
    
    ГотовыйСкрипт = СтрЗаменить(СкриптHTML, "&Данные&", ЗаписьРезультатаЗапроса.Закрыть());
    
    Дерево = ГотовыйСкрипт;
    
КонецПроцедуры
