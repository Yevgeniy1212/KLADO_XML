
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДСРаспределенияДоговора, "Договор", НЕОПРЕДЕЛЕНО);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗатраты(Команда)
	ЗаполнитьЗатратыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗатратыНаСервере()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ПодразделениеОрганизации", ПодразделениеОрганизации);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗатратыИзУчетнойСистемыОбороты.Организация КАК Организация,
	               |	ЗатратыИзУчетнойСистемыОбороты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	               |	ЗатратыИзУчетнойСистемыОбороты.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ЗатратыИзУчетнойСистемыОбороты.Контрагент КАК Контрагент,
	               |	ЗатратыИзУчетнойСистемыОбороты.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ЗатратыИзУчетнойСистемыОбороты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	               |	ЗатратыИзУчетнойСистемыОбороты.СуммаЗатратОборот КАК СуммаЗатратОборот
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	РегистрНакопления.ЗатратыИзУчетнойСистемы.Обороты(&ДатаНачала, &ДатаОкончания, , ПодразделениеОрганизации = &ПодразделениеОрганизации) КАК ЗатратыИзУчетнойСистемыОбороты
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Организация,
	               |	ДоговорИзУчетнойСистемы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СоответствиеДоговоров.Организация КАК Организация,
	               |	СоответствиеДоговоров.Договор КАК Договор,
	               |	СоответствиеДоговоров.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы
	               |ПОМЕСТИТЬ ВТ_соотв
	               |ИЗ
	               |	РегистрСведений.СоответствиеДоговоров КАК СоответствиеДоговоров
	               |ГДЕ
	               |	(СоответствиеДоговоров.Организация, СоответствиеДоговоров.ДоговорИзУчетнойСистемы) В
	               |			(ВЫБРАТЬ
	               |				ВТ.Организация КАК Организация,
	               |				ВТ.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы
	               |			ИЗ
	               |				ВТ КАК ВТ)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Организация,
	               |	ДоговорИзУчетнойСистемы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_соотв.Договор КАК Договор,
	               |	ВТ_соотв.Организация КАК Организация,
	               |	ВТ_соотв.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	СУММА(ДоговорыРаспределения.Коэффициент) КАК Коэффициент,
	               |	ДоговорыРаспределения.Ссылка.СуммаНачисления КАК СуммаНачисления,
	               |	ДоговорыРаспределения.Ссылка.СуммаРаспределенная КАК СуммаРаспределенная
	               |ПОМЕСТИТЬ ВТ_Распределения
	               |ИЗ
	               |	ВТ_соотв КАК ВТ_соотв
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Договоры.Распределения КАК ДоговорыРаспределения
	               |		ПО ВТ_соотв.Договор = ДоговорыРаспределения.Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_соотв.Организация,
	               |	ВТ_соотв.Договор,
	               |	ВТ_соотв.ДоговорИзУчетнойСистемы,
	               |	ДоговорыРаспределения.Ссылка.СуммаНачисления,
	               |	ДоговорыРаспределения.Ссылка.СуммаРаспределенная
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.Организация КАК Организация,
	               |	ВТ.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	               |	ВТ.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ВТ.Контрагент КАК Контрагент,
	               |	ВТ.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ВТ.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	               |	ВТ.СуммаЗатратОборот КАК СуммаЗатрат,
	               |	ЕСТЬNULL(ВТ_Распределения.Коэффициент, 0) КАК Коэффициент,
	               |	ЕСТЬNULL(ВТ_Распределения.СуммаНачисления, 0) КАК СуммаНачисления,
	               |	ЕСТЬNULL(ВТ_Распределения.Договор, НЕОПРЕДЕЛЕНО) КАК Договор,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(ВТ_Распределения.Коэффициент, 0) < 100
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК Пометка
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Распределения КАК ВТ_Распределения
	               |		ПО ВТ.ДоговорИзУчетнойСистемы = ВТ_Распределения.ДоговорИзУчетнойСистемы
	               |			И ВТ.Организация = ВТ_Распределения.Организация";
	
	Если Не ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПодразделениеОрганизации = &ПодразделениеОрганизации", "");
	КонецЕсли;
	Объект.ЗатратыИзУчетнойСистемы.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

&НаКлиенте
Процедура ЗатратыИзУчетнойСистемыПриАктивизацииСтроки(Элемент)
	мТекущаяСтрока =  Элементы.ЗатратыИзУчетнойСистемы.ТекущиеДанные; 
	Если мТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДСРаспределенияДоговора, "Договор", мТекущаяСтрока.Договор);
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДокументыРаспределения, "Дата", Период.ДатаНачала, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДокументыРаспределения, "Дата", Период.ДатаОкончания, ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыРаспределения(Команда)
	СоздатьДокументыРаспределенияНаСервере();
	Элементы.ДокументыРаспределения.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ЗатратыИзУчетнойСистемыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	мТекущаяСтрока =  Элементы.ЗатратыИзУчетнойСистемы.ТекущиеДанные; 
	Если Поле.Имя = "ЗатратыИзУчетнойСистемыДоговор" Тогда
		ПоказатьЗначение(, мТекущаяСтрока.Договор);
	Иначе
		ПоказатьЗначение(, мТекущаяСтрока.ДоговорИзУчетнойСистемы);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыРаспределенияНаСервере()
	
	Запрос = Новый Запрос;
	МВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.УстановитьПараметр("ТЗ", Объект.ЗатратыИзУчетнойСистемы.Выгрузить());
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТЗ.Организация КАК Организация,
	               |	ТЗ.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	               |	ТЗ.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ТЗ.Контрагент КАК Контрагент,
	               |	ТЗ.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ТЗ.СуммаЗатрат КАК СуммаЗатрат,
	               |	ТЗ.Договор КАК Договор,
	               |	ТЗ.СуммаНачисления КАК СуммаНачисления,
	               |	ТЗ.Коэффициент КАК Коэффициент,
	               |	ТЗ.Пометка КАК Пометка
	               |ПОМЕСТИТЬ ТЗ
	               |ИЗ
	               |	&ТЗ КАК ТЗ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДоговорыРаспределения.Ссылка КАК Договор,
	               |	ДоговорыРаспределения.Договор КАК ДоговорДохода,
	               |	ДоговорыРаспределения.Коэффициент КАК Коэффициент
	               |ПОМЕСТИТЬ ВТ_Распределения
	               |ИЗ
	               |	Справочник.Договоры.Распределения КАК ДоговорыРаспределения
	               |ГДЕ
	               |	ДоговорыРаспределения.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ТЗ.Договор КАК Договор
	               |			ИЗ
	               |				ТЗ КАК ТЗ
	               |			ГДЕ
	               |				ТЗ.Пометка)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Распределения.Договор КАК Договор,
	               |	ВТ_Распределения.ДоговорДохода КАК ДоговорДохода,
	               |	СУММА(ВТ_Распределения.Коэффициент) КАК Коэффициент,
	               |	ЕСТЬNULL(СоответствиеДоговоров.ДоговорИзУчетнойСистемы, НЕОПРЕДЕЛЕНО) КАК ДоговорДоходаУС
	               |ПОМЕСТИТЬ ВТ_Распределения2
	               |ИЗ
	               |	ВТ_Распределения КАК ВТ_Распределения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеДоговоров КАК СоответствиеДоговоров
	               |		ПО ВТ_Распределения.ДоговорДохода = СоответствиеДоговоров.Договор
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Распределения.ДоговорДохода,
	               |	ВТ_Распределения.Договор,
	               |	ЕСТЬNULL(СоответствиеДоговоров.ДоговорИзУчетнойСистемы, НЕОПРЕДЕЛЕНО)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТЗ.Организация КАК Организация,
	               |	ТЗ.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ТЗ.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ТЗ.СуммаЗатрат КАК СуммаЗатрат,
	               |	ТЗ.Договор КАК Договор,
	               |	ЕСТЬNULL(РапределениеЗатратПоДоговорамУчетнойСистемы.Ссылка, НЕОПРЕДЕЛЕНО) КАК Документ
	               |ПОМЕСТИТЬ ВТ_итог
	               |ИЗ
	               |	ТЗ КАК ТЗ
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РапределениеЗатратПоДоговорамУчетнойСистемы КАК РапределениеЗатратПоДоговорамУчетнойСистемы
	               |		ПО ТЗ.Организация = РапределениеЗатратПоДоговорамУчетнойСистемы.Организация
	               |			И ТЗ.ДоговорИзУчетнойСистемы = РапределениеЗатратПоДоговорамУчетнойСистемы.Договор
	               |			И ТЗ.СтатьяЗатрат = РапределениеЗатратПоДоговорамУчетнойСистемы.СтатьяЗатрат
	               |			И (НЕ РапределениеЗатратПоДоговорамУчетнойСистемы.ПометкаУдаления)
	               |ГДЕ
	               |	ТЗ.Пометка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_итог.Организация КАК Организация,
	               |	ВТ_итог.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ВТ_итог.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ВТ_итог.СуммаЗатрат КАК СуммаЗатрат,
	               |	ВТ_итог.Договор КАК Договор,
	               |	ВТ_итог.Документ КАК ДокументРаспределения,
	               |	ВТ_Распределения2.ДоговорДохода КАК ДоговорДоходаКЛАДО,
	               |	ВТ_Распределения2.Коэффициент КАК Коэффициент,
	               |	ВТ_Распределения2.ДоговорДоходаУС КАК ДоговорДохода
	               |ИЗ
	               |	ВТ_итог КАК ВТ_итог
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Распределения2 КАК ВТ_Распределения2
	               |		ПО ВТ_итог.Договор = ВТ_Распределения2.Договор
	               |ИТОГИ
	               |	МАКСИМУМ(СуммаЗатрат)
	               |ПО
	               |	ДоговорИзУчетнойСистемы,
	               |	СтатьяЗатрат,
	               |	ДокументРаспределения";
	
	мТЗ = Новый ТаблицаЗначений;
	мТЗ.Колонки.Добавить("ДоговорДохода"); 
	мТЗ.Колонки.Добавить("СуммаЗатрат"); 
	мТЗ.Колонки.Добавить("Коэффициент"); 
	
	ОрганизацияПоУмолчанию = КЛАДОСлужебныйПовтИсп.ОрганизацияПоУмолчанию();
	
	ВыборкаДоговорУС = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДоговорУС.Следующий() Цикл
		
		ВыборкаСтатья = ВыборкаДоговорУС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСтатья.Следующий() Цикл
			ВыборкаДокумент = ВыборкаСтатья.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДокумент.Следующий() Цикл
				
				мТЗ.Очистить();
				
				Если ЗначениеЗаполнено(ВыборкаДокумент.ДокументРаспределения) Тогда
					ТекДокумент = ВыборкаДокумент.ДокументРаспределения.ПолучитьОбъект();
					//Если ВыборкаДокумент.ПравилоРаспределения = Неопределено Тогда
					//	//удаляем документ
					//	ТекДокумент.УстановитьПометкуУдаления(Истина);
					//	Продолжить;
					//КонецЕсли;
				Иначе
					ТекДокумент = Документы.РапределениеЗатратПоДоговорамУчетнойСистемы.СоздатьДокумент();
					ТекДокумент.Дата = Период.ДатаОкончания;
					ТекДокумент.Организация = ОрганизацияПоУмолчанию;
					ТекДокумент.Договор = ВыборкаДоговорУС.ДоговорИзУчетнойСистемы;
					ТекДокумент.СтатьяЗатрат = ВыборкаСтатья.СтатьяЗатрат;
				КонецЕсли;
				
				ТекДокумент.СуммаЗатрат = ВыборкаДоговорУС.СуммаЗатрат;
				Если ТекДокумент.Проведен Тогда 
					ТекДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				КонецЕсли;
				
				Выборка = ВыборкаДокумент.Выбрать();
				Пока Выборка.Следующий() Цикл
					Стр = мТЗ.Добавить();
					ЗаполнитьЗначенияСвойств(Стр, Выборка, , "СуммаЗатрат");
				КонецЦикла;
			
				ТекДокумент.Затраты.Очистить();
				
				СуммаОстаток = ВыборкаДоговорУС.СуммаЗатрат;
				Если мТЗ.Количество() > 0 Тогда
					Для Каждого Стр ИЗ мТЗ Цикл
						Стр.СуммаЗатрат = Окр(Стр.Коэффициент * СуммаОстаток/100); 
						СуммаОстаток = СуммаОстаток - Стр.СуммаЗатрат;
					КонецЦикла;
					Стр.СуммаЗатрат = Стр.СуммаЗатрат + СуммаОстаток; 
					ТекДокумент.Затраты.Загрузить(мТЗ);
				КонецЕсли;
				
				ТекДокумент.Записать(РежимЗаписиДокумента.Запись);
			КонецЦикла;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	Для Каждого Строка ИЗ Объект.ЗатратыИзУчетнойСистемы Цикл
		Если Строка.Коэффициент < 100 тогда
			Продолжить;
		КонецЕсли;
		Строка.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсе(Команда)
	Для Каждого Строка ИЗ Объект.ЗатратыИзУчетнойСистемы Цикл
		Если Строка.Коэффициент < 100 тогда
			Продолжить;
		КонецЕсли;
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры



