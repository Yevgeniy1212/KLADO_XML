
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗатраты(Команда)
	ЗаполнитьЗатратыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗатратыНаСервере()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ПодразделениеОрганизации", ПодразделениеОрганизации);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗатратыИзУчетнойСистемыОбороты.Организация КАК Организация,
	               |	ЗатратыИзУчетнойСистемыОбороты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	               |	ЗатратыИзУчетнойСистемыОбороты.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ЗатратыИзУчетнойСистемыОбороты.Контрагент КАК Контрагент,
	               |	ЗатратыИзУчетнойСистемыОбороты.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ЗатратыИзУчетнойСистемыОбороты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	               |	ЗатратыИзУчетнойСистемыОбороты.СуммаЗатратОборот КАК СуммаЗатратОборот
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	РегистрНакопления.ЗатратыИзУчетнойСистемы.Обороты(&ДатаНачала, &ДатаОкончания, , ПодразделениеОрганизации = &ПодразделениеОрганизации) КАК ЗатратыИзУчетнойСистемыОбороты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПравилаРаспределения.Ссылка КАК Ссылка,
	               |	ПравилаРаспределения.Организация КАК Организация,
	               |	ПравилаРаспределения.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	               |	ПравилаРаспределения.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ПравилаРаспределения.Контрагент КАК Контрагент,
	               |	ПравилаРаспределения.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ПравилаРаспределения.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа
	               |ПОМЕСТИТЬ ВТ_Правила
	               |ИЗ
	               |	Справочник.ПравилаРаспределения КАК ПравилаРаспределения
	               |ГДЕ
	               |	ПравилаРаспределения.Действует
	               |	И НЕ ПравилаРаспределения.ПометкаУдаления
	               |	И ПравилаРаспределения.ПодразделениеОрганизации = &ПодразделениеОрганизации
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.Организация КАК Организация,
	               |	ВТ.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	               |	ВТ.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ВТ.Контрагент КАК Контрагент,
	               |	ВТ.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ВТ.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	               |	ВТ.СуммаЗатратОборот КАК СуммаЗатратОборот,
	               |	НЕОПРЕДЕЛЕНО КАК ПравилоРаспределения,
	               |	99 КАК Приоритет
	               |ПОМЕСТИТЬ ВТ_Правила1
	               |ИЗ
	               |	ВТ КАК ВТ
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ.Организация,
	               |	ВТ.ПодразделениеОрганизации,
	               |	ВТ.СтатьяЗатрат,
	               |	ВТ.Контрагент,
	               |	ВТ.ДоговорИзУчетнойСистемы,
	               |	ВТ.НоменклатурнаяГруппа,
	               |	ВТ.СуммаЗатратОборот,
	               |	ВТ_Правила.Ссылка,
	               |	0
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Правила КАК ВТ_Правила
	               |		ПО ВТ.ПодразделениеОрганизации = ВТ_Правила.ПодразделениеОрганизации
	               |			И ВТ.Организация = ВТ_Правила.Организация
	               |			И ВТ.СтатьяЗатрат = ВТ_Правила.СтатьяЗатрат
	               |			И ВТ.Контрагент = ВТ_Правила.Контрагент
	               |			И ВТ.ДоговорИзУчетнойСистемы = ВТ_Правила.ДоговорИзУчетнойСистемы
	               |			И ВТ.НоменклатурнаяГруппа = ВТ_Правила.НоменклатурнаяГруппа
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ.Организация,
	               |	ВТ.ПодразделениеОрганизации,
	               |	ВТ.СтатьяЗатрат,
	               |	ВТ.Контрагент,
	               |	ВТ.ДоговорИзУчетнойСистемы,
	               |	ВТ.НоменклатурнаяГруппа,
	               |	ВТ.СуммаЗатратОборот,
	               |	ВТ_Правила.Ссылка,
	               |	1
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Правила КАК ВТ_Правила
	               |		ПО ВТ.Организация = ВТ_Правила.Организация
	               |			И ВТ.ПодразделениеОрганизации = ВТ_Правила.ПодразделениеОрганизации
	               |			И ВТ.СтатьяЗатрат = ВТ_Правила.СтатьяЗатрат
	               |			И ВТ.Контрагент = ВТ_Правила.Контрагент
	               |			И (ВТ_Правила.ДоговорИзУчетнойСистемы = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	               |			И (ВТ_Правила.НоменклатурнаяГруппа = ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка))
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ.Организация,
	               |	ВТ.ПодразделениеОрганизации,
	               |	ВТ.СтатьяЗатрат,
	               |	ВТ.Контрагент,
	               |	ВТ.ДоговорИзУчетнойСистемы,
	               |	ВТ.НоменклатурнаяГруппа,
	               |	ВТ.СуммаЗатратОборот,
	               |	ВТ_Правила.Ссылка,
	               |	2
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Правила КАК ВТ_Правила
	               |		ПО ВТ.ПодразделениеОрганизации = ВТ_Правила.ПодразделениеОрганизации
	               |			И ВТ.Организация = ВТ_Правила.Организация
	               |			И ВТ.СтатьяЗатрат = ВТ_Правила.СтатьяЗатрат
	               |			И (ВТ_Правила.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	               |			И (ВТ_Правила.ДоговорИзУчетнойСистемы = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	               |			И (ВТ_Правила.НоменклатурнаяГруппа = ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка))
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ.Организация,
	               |	ВТ.ПодразделениеОрганизации,
	               |	ВТ.СтатьяЗатрат,
	               |	ВТ.Контрагент,
	               |	ВТ.ДоговорИзУчетнойСистемы,
	               |	ВТ.НоменклатурнаяГруппа,
	               |	ВТ.СуммаЗатратОборот,
	               |	ВТ_Правила.Ссылка,
	               |	3
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Правила КАК ВТ_Правила
	               |		ПО ВТ.Организация = ВТ_Правила.Организация
	               |			И ВТ.СтатьяЗатрат = ВТ_Правила.СтатьяЗатрат
	               |			И (ВТ_Правила.ДоговорИзУчетнойСистемы = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	               |			И (ВТ_Правила.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	               |			И (ВТ_Правила.ПодразделениеОрганизации = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	               |			И (ВТ_Правила.НоменклатурнаяГруппа = ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Правила1.Организация КАК Организация,
	               |	ВТ_Правила1.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	               |	ВТ_Правила1.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ВТ_Правила1.Контрагент КАК Контрагент,
	               |	ВТ_Правила1.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ВТ_Правила1.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	               |	МИНИМУМ(ВТ_Правила1.Приоритет) КАК Приоритет
	               |ПОМЕСТИТЬ ВТ_Максимум
	               |ИЗ
	               |	ВТ_Правила1 КАК ВТ_Правила1
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Правила1.ПодразделениеОрганизации,
	               |	ВТ_Правила1.Контрагент,
	               |	ВТ_Правила1.СтатьяЗатрат,
	               |	ВТ_Правила1.НоменклатурнаяГруппа,
	               |	ВТ_Правила1.Организация,
	               |	ВТ_Правила1.ДоговорИзУчетнойСистемы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Правила1.Организация КАК Организация,
	               |	ВТ_Правила1.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	               |	ВТ_Правила1.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ВТ_Правила1.Контрагент КАК Контрагент,
	               |	ВТ_Правила1.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ВТ_Правила1.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	               |	ВТ_Правила1.СуммаЗатратОборот КАК СуммаЗатрат,
	               |	МАКСИМУМ(ВТ_Правила1.ПравилоРаспределения) КАК ПравилоРаспределения,
	               |	ВТ_Правила1.Приоритет КАК Приоритет
	               |ИЗ
	               |	ВТ_Правила1 КАК ВТ_Правила1
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Максимум КАК ВТ_Максимум
	               |		ПО ВТ_Правила1.Организация = ВТ_Максимум.Организация
	               |			И ВТ_Правила1.ПодразделениеОрганизации = ВТ_Максимум.ПодразделениеОрганизации
	               |			И ВТ_Правила1.СтатьяЗатрат = ВТ_Максимум.СтатьяЗатрат
	               |			И ВТ_Правила1.Контрагент = ВТ_Максимум.Контрагент
	               |			И ВТ_Правила1.ДоговорИзУчетнойСистемы = ВТ_Максимум.ДоговорИзУчетнойСистемы
	               |			И ВТ_Правила1.НоменклатурнаяГруппа = ВТ_Максимум.НоменклатурнаяГруппа
	               |			И ВТ_Правила1.Приоритет = ВТ_Максимум.Приоритет
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Правила1.ПодразделениеОрганизации,
	               |	ВТ_Правила1.ДоговорИзУчетнойСистемы,
	               |	ВТ_Правила1.Организация,
	               |	ВТ_Правила1.СтатьяЗатрат,
	               |	ВТ_Правила1.Контрагент,
	               |	ВТ_Правила1.НоменклатурнаяГруппа,
	               |	ВТ_Правила1.СуммаЗатратОборот,
	               |	ВТ_Правила1.Приоритет";
	
	Если Не ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ПравилаРаспределения.ПодразделениеОрганизации = &ПодразделениеОрганизации", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПодразделениеОрганизации = &ПодразделениеОрганизации", "");
	КонецЕсли;
	Объект.ЗатратыИзУчетнойСистемы.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры


&НаСервере
Процедура ЗаполнитьТЧПравилаРаспределения(ПравилоРаспределения)
	
	//ТекущееПравилоРаспределенияОб = ТекущееПравилоРаспределения.ПолучитьОбъект(Объект.Договора.Выгрузить());
	//ТекущееПравилоРаспределенияОб.Распределение.Загрузить();
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПравилоРаспределения", ПравилоРаспределения);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПравилаРаспределенияРаспределение.Ссылка КАК ПравилоРаспределения,
	               |	ПравилаРаспределенияРаспределение.НомерСтроки КАК НомерСтроки,
	               |	ПравилаРаспределенияРаспределение.Договор КАК Договор,
	               |	ПравилаРаспределенияРаспределение.Подразделение КАК Подразделение,
	               |	ПравилаРаспределенияРаспределение.Коэффициент КАК Коэффициент
	               |ИЗ
	               |	Справочник.ПравилаРаспределения.Распределение КАК ПравилаРаспределенияРаспределение
	               |ГДЕ
	               |	ПравилаРаспределенияРаспределение.Ссылка = &ПравилоРаспределения";
	Объект.Договора.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТЧПравилаРаспределенияПриАктивизации()
	Если ЕстьИзмененияПравила Тогда
		ЗаписатьИзмененияПравила();		
	КонецЕсли;
	ЕстьИзмененияПравила = Ложь;
	ТекущаяСтрока = Элементы.ЗатратыИзУчетнойСистемы.ТекущаяСтрока;
	ТекущееПравилоРаспределения = Элементы.ЗатратыИзУчетнойСистемы.ТекущиеДанные.ПравилоРаспределения;
	ЗаполнитьТЧПравилаРаспределения(ТекущееПравилоРаспределения);
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияПравилаНаСервере(ТекущееПравилоРаспределения, СтруктураРеквизитов = Неопределено)
	
	Если ЗначениеЗаполнено(ТекущееПравилоРаспределения) Тогда
		ТекущееПравилоРаспределенияОб = ТекущееПравилоРаспределения.ПолучитьОбъект();
	ИначеЕсли НЕ СтруктураРеквизитов = Неопределено Тогда
		ТекущееПравилоРаспределенияОб = Справочники.ПравилаРаспределения.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(ТекущееПравилоРаспределенияОб, СтруктураРеквизитов);
		ТекущееПравилоРаспределенияОб.Действует = Истина;
	Иначе Возврат;
	КонецЕсли;
	ТекущееПравилоРаспределенияОб.Распределение.Загрузить(Объект.Договора.Выгрузить());
	ТекущееПравилоРаспределенияОб.Записать();
	ТекущееПравилоРаспределения = ТекущееПравилоРаспределенияОб.Ссылка;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзмененияПравила()
	ТекущиеДанныеЗатрат = Объект.ЗатратыИзУчетнойСистемы.НайтиПоИдентификатору(ТекущаяСтрока);
	СтруктураР = Неопределено;
	ПравилоДляТекНабора = Ложь;
	Если ТекущиеДанныеЗатрат.Приоритет <> 0 Тогда
		//создаем новое правило
		ТекущиеДанныеЗатрат.ПравилоРаспределения = Неопределено;
		ТекущееПравилоРаспределения = Неопределено;
	КонецЕсли;
	Если НЕ ТекущиеДанныеЗатрат =Неопределено 
		И НЕ ЗначениеЗаполнено(ТекущееПравилоРаспределения) Тогда
		СтруктураР = Новый Структура("Организация,ПодразделениеОрганизации,СтатьяЗатрат, Контрагент, ДоговорИзУчетнойСистемы, НоменклатурнаяГруппа");
		ЗаполнитьЗначенияСвойств(СтруктураР, ТекущиеДанныеЗатрат);
		ПравилоДляТекНабора = Истина;
	КонецЕсли;
	ЗаписатьИзмененияПравилаНаСервере(ТекущееПравилоРаспределения, СтруктураР);
	Если НЕ ТекущиеДанныеЗатрат =Неопределено Тогда
		ТекущиеДанныеЗатрат.ПравилоРаспределения = ТекущееПравилоРаспределения;
		Если ПравилоДляТекНабора Тогда
			ТекущиеДанныеЗатрат.Приоритет = 0;
		КонецЕсли;
	КонецЕсли;
	ТекущиеДанные = Элементы.ЗатратыИзУчетнойСистемы.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ПравилоРаспределения) Тогда
		ЕстьИзмененияПравила = Ложь;
		ТекущаяСтрока = Элементы.ЗатратыИзУчетнойСистемы.ТекущаяСтрока;
		ТекущееПравилоРаспределения = ТекущиеДанные.ПравилоРаспределения;
		Объект.Договора.Очистить();
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ЗатратыИзУчетнойСистемыПриАктивизацииСтроки(Элемент)
	мТекущаяСтрока =  Элементы.ЗатратыИзУчетнойСистемы.ТекущаяСтрока; 
	Если мТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПредДанныеЗатрат = Объект.ЗатратыИзУчетнойСистемы.НайтиПоИдентификатору(ТекущаяСтрока);
	ТекущиеДанные = Элементы.ЗатратыИзУчетнойСистемы.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущееПравилоРаспределения) И ТекущееПравилоРаспределения = ТекущиеДанные.ПравилоРаспределения
		И НЕ ПредДанныеЗатрат = Неопределено И ПредДанныеЗатрат.Приоритет = 0 Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ПравилоРаспределения) Тогда
		Если ЕстьИзмененияПравила Тогда
			ПодключитьОбработчикОжидания("ЗаписатьИзмененияПравила", 0.1, Истина);		
		Иначе
			ЕстьИзмененияПравила = Ложь;
			ТекущаяСтрока = Элементы.ЗатратыИзУчетнойСистемы.ТекущаяСтрока;
			ТекущееПравилоРаспределения = ТекущиеДанные.ПравилоРаспределения;
			Объект.Договора.Очистить();
		КонецЕсли;
	Иначе
		ПодключитьОбработчикОжидания("ЗаполнитьТЧПравилаРаспределенияПриАктивизации", 0.1, Истина);		
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ДоговораПриИзменении(Элемент)
	ЕстьИзмененияПравила = Истина;
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьПодразделение(Договор)
	Возврат Договор.Подразделение;
КонецФункции


&НаКлиенте
Процедура ДоговораДоговорПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Договора.ТекущиеДанные;
	ТекущиеДанные.Подразделение = ПолучитьПодразделение(ТекущиеДанные.Договор);
	ТекущиеДанные.Коэффициент = 1;
КонецПроцедуры


&НаКлиенте
Процедура ДоговораПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	Для Каждого СтрокаТек Из ПараметрыПеретаскивания.Значение Цикл
		//Строки = Объект.Договора.НайтиСтроки(Новый Структура("Договор", Подразделение"), );
		НоваяСтрока = Объект.Договора.Добавить();
		НоваяСтрока.Договор = СтрокаТек;
		НоваяСтрока.Подразделение = ПолучитьПодразделение(СтрокаТек);
		НоваяСтрока.Коэффициент = 1;
	КонецЦикла;
	ЕстьИзмененияПравила = Истина;
КонецПроцедуры


&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДокументыРаспределения, "Дата", Период.ДатаНачала, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДокументыРаспределения, "Дата", Период.ДатаОкончания, ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
КонецПроцедуры


&НаКлиенте
Процедура ЗатратыИзУчетнойСистемыПравилоРаспределенияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ЗатратыИзУчетнойСистемы.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущееПравилоРаспределения) И ТекущееПравилоРаспределения = ТекущиеДанные.ПравилоРаспределения Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ПравилоРаспределения) Тогда
		Если ЕстьИзмененияПравила Тогда
			ЗаписатьИзмененияПравила();		
		Иначе
			ЕстьИзмененияПравила = Ложь;
			ТекущаяСтрока = Элементы.ЗатратыИзУчетнойСистемы.ТекущаяСтрока;
			ТекущееПравилоРаспределения = ТекущиеДанные.ПравилоРаспределения;
			Объект.Договора.Очистить();
		КонецЕсли;
	Иначе
		ЗаполнитьТЧПравилаРаспределенияПриАктивизации();		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения()
	мТекущаяСтрока =  Элементы.ЗатратыИзУчетнойСистемы.ТекущаяСтрока; 
	Если НЕ мТекущаяСтрока = Неопределено Тогда
		ТекущиеДанные = Элементы.ЗатратыИзУчетнойСистемы.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекущееПравилоРаспределения) И ТекущееПравилоРаспределения = ТекущиеДанные.ПравилоРаспределения
			И ТекущиеДанные.Приоритет = 0 Тогда
			Возврат;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ПравилоРаспределения) Тогда
			Если ЕстьИзмененияПравила Тогда
				ЗаписатьИзмененияПравила();		
			Иначе
				ЕстьИзмененияПравила = Ложь;
				ТекущаяСтрока = Элементы.ЗатратыИзУчетнойСистемы.ТекущаяСтрока;
				ТекущееПравилоРаспределения = ТекущиеДанные.ПравилоРаспределения;
				Объект.Договора.Очистить();
			КонецЕсли;
		Иначе
			ЗаполнитьТЧПравилаРаспределенияПриАктивизации();		
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура СоздатьДокументыРаспределения(Команда)
	СохранитьИзменения();
	СоздатьДокументыРаспределенияНаСервере();
	Элементы.ДокументыРаспределения.Обновить();
КонецПроцедуры


&НаСервере
Процедура СоздатьДокументыРаспределенияНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТЗ", Объект.ЗатратыИзУчетнойСистемы.Выгрузить());
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТЗ.ПравилоРаспределения КАК ПравилоРаспределения,
	               |	ТЗ.Организация КАК Организация,
	               |	ТЗ.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	               |	ТЗ.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ТЗ.Контрагент КАК Контрагент,
	               |	ТЗ.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы,
	               |	ТЗ.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	               |	ТЗ.СуммаЗатрат КАК СуммаЗатрат
	               |ПОМЕСТИТЬ ТЗ
	               |ИЗ
	               |	&ТЗ КАК ТЗ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТЗ.ПравилоРаспределения КАК ПравилоРаспределения,
	               |	ТЗ.Организация КАК Организация,
	               |	ТЗ.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	СУММА(ТЗ.СуммаЗатрат) КАК СуммаЗатрат
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	ТЗ КАК ТЗ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТЗ.ПравилоРаспределения,
	               |	ТЗ.Организация,
	               |	ТЗ.СтатьяЗатрат
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.ПравилоРаспределения КАК ПравилоРаспределения,
	               |	ВТ.Организация КАК Организация,
	               |	ВТ.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ВТ.СуммаЗатрат КАК СуммаЗатрат,
	               |	ПравилаРаспределенияРаспределение.НомерСтроки КАК НомерСтроки,
	               |	ПравилаРаспределенияРаспределение.Договор КАК Договор,
	               |	ПравилаРаспределенияРаспределение.Подразделение КАК Подразделение,
	               |	ПравилаРаспределенияРаспределение.Коэффициент КАК Коэффициент
	               |ПОМЕСТИТЬ ВТ_Распределения
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределения.Распределение КАК ПравилаРаспределенияРаспределение
	               |		ПО ВТ.ПравилоРаспределения = ПравилаРаспределенияРаспределение.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РапределениеЗатратПоДоговорам.Ссылка КАК Ссылка,
	               |	РапределениеЗатратПоДоговорам.Организация КАК Организация,
	               |	РапределениеЗатратПоДоговорам.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	РапределениеЗатратПоДоговорам.ПравилоРаспределения КАК ПравилоРаспределения
	               |ПОМЕСТИТЬ ВТ_документы
	               |ИЗ
	               |	Документ.РапределениеЗатратПоДоговорам КАК РапределениеЗатратПоДоговорам
	               |ГДЕ
	               |	РапределениеЗатратПоДоговорам.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	               |	И НЕ РапределениеЗатратПоДоговорам.ПометкаУдаления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(ВложенныйЗапрос.ДокументРаспределения) КАК ДокументРаспределения,
	               |	ВложенныйЗапрос.Организация КАК Организация,
	               |	ВложенныйЗапрос.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ВложенныйЗапрос.ПравилоРаспределения КАК ПравилоРаспределения
	               |ПОМЕСТИТЬ ВТ_Данные
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ВТ_документы.Ссылка КАК ДокументРаспределения,
	               |		ВТ_документы.Организация КАК Организация,
	               |		ВТ_документы.СтатьяЗатрат КАК СтатьяЗатрат,
	               |		ВТ_документы.ПравилоРаспределения КАК ПравилоРаспределения
	               |	ИЗ
	               |		ВТ_документы КАК ВТ_документы
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		НЕОПРЕДЕЛЕНО,
	               |		ВТ.Организация,
	               |		ВТ.СтатьяЗатрат,
	               |		ВТ.ПравилоРаспределения
	               |	ИЗ
	               |		ВТ КАК ВТ) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.ПравилоРаспределения,
	               |	ВложенныйЗапрос.Организация,
	               |	ВложенныйЗапрос.СтатьяЗатрат
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Данные.ДокументРаспределения КАК ДокументРаспределения,
	               |	ВТ_Данные.Организация КАК Организация,
	               |	ВТ_Данные.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	ЕСТЬNULL(ВТ_Распределения.ПравилоРаспределения, НЕОПРЕДЕЛЕНО) КАК ПравилоРаспределения,
	               |	ВТ_Распределения.НомерСтроки КАК НомерСтроки,
	               |	ВТ_Распределения.Договор КАК Договор,
	               |	ВТ_Распределения.Подразделение КАК Подразделение,
	               |	ЕСТЬNULL(ВТ_Распределения.Коэффициент, 0) КАК Коэффициент,
	               |	ЕСТЬNULL(ВТ_Распределения.СуммаЗатрат, 0) КАК СуммаЗатрат
	               |ИЗ
	               |	ВТ_Данные КАК ВТ_Данные
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Распределения КАК ВТ_Распределения
	               |		ПО ВТ_Данные.Организация = ВТ_Распределения.Организация
	               |			И ВТ_Данные.СтатьяЗатрат = ВТ_Распределения.СтатьяЗатрат
	               |			И ВТ_Данные.ПравилоРаспределения = ВТ_Распределения.ПравилоРаспределения
	               |ГДЕ
	               |	ЕСТЬNULL(ВТ_Распределения.ПравилоРаспределения, НЕОПРЕДЕЛЕНО) <> НЕОПРЕДЕЛЕНО
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ДокументРаспределения
	               |ИТОГИ
	               |	СУММА(Коэффициент),
	               |	СУММА(СуммаЗатрат)
	               |ПО
	               |	ПравилоРаспределения,
	               |	ДокументРаспределения";
	
	мТЗ = Новый ТаблицаЗначений;
	мТЗ.Колонки.Добавить("Подразделение"); 
	мТЗ.Колонки.Добавить("Договор"); 
	мТЗ.Колонки.Добавить("СуммаЗатрат"); 
	мТЗ.Колонки.Добавить("Коэффициент"); 
	
	ВыборкаПравило = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПравило.Следующий() Цикл
		ВыборкаДокумент = ВыборкаПравило.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДокумент.Следующий() Цикл
			
			мТЗ.Очистить();
			
			Если ЗначениеЗаполнено(ВыборкаДокумент.ДокументРаспределения) Тогда
				ТекДокумент = ВыборкаДокумент.ДокументРаспределения.ПолучитьОбъект();
				Если ВыборкаДокумент.ПравилоРаспределения = Неопределено Тогда
					//удаляем документ
					ТекДокумент.УстановитьПометкуУдаления(Истина);
					Продолжить;
				КонецЕсли;
			Иначе
				ТекДокумент = Документы.РапределениеЗатратПоДоговорам.СоздатьДокумент();
				ТекДокумент.Дата = Период.ДатаОкончания;
			КонецЕсли;
			
			Если ТекДокумент.Проведен Тогда 
				ТекДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			КонецЕсли;
			
			ТекДокумент.ПравилоРаспределения = ВыборкаДокумент.ПравилоРаспределения;
			
			СтруктураЗаполнения = Новый структура("Организация, СтатьяЗатрат");
			
			Выборка = ВыборкаДокумент.Выбрать();
			Пока Выборка.Следующий() Цикл
				СтруктураЗаполнения.Организация = Выборка.Организация;
				СтруктураЗаполнения.СтатьяЗатрат = Выборка.СтатьяЗатрат;
				
				Стр = мТЗ.Добавить();
				ЗаполнитьЗначенияСвойств(Стр, Выборка, , "СуммаЗатрат");
			КонецЦикла;
			
			ТекДокумент.Затраты.Очистить();
			
			СуммаОстаток = ВыборкаПравило.СуммаЗатрат;
			СуммаКоэфф = ВыборкаПравило.Коэффициент;
			Если мТЗ.Количество() > 0 И НЕ СуммаКоэфф = 0 Тогда
				Для Каждого Стр ИЗ мТЗ Цикл
					Стр.СуммаЗатрат = Окр(Стр.Коэффициент / СуммаКоэфф * ВыборкаПравило.СуммаЗатрат); 
					СуммаОстаток = СуммаОстаток - Стр.СуммаЗатрат;
				КонецЦикла;
				Стр.СуммаЗатрат = Стр.СуммаЗатрат + СуммаОстаток; 
				ТекДокумент.Затраты.Загрузить(мТЗ);
			КонецЕсли;
			
			
			ЗаполнитьЗначенияСвойств(ТекДокумент, СтруктураЗаполнения);
			
			ТекДокумент.Записать(РежимЗаписиДокумента.Запись);
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура ПереместитьВлево(Команда)
	ВыбранныеСтроки = Элементы.ДСДоговоры.ВыделенныеСтроки;
	Если ВыбранныеСтроки.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	Для Каждого СтрокаТек Из ВыбранныеСтроки Цикл
		НоваяСтрока = Объект.Договора.Добавить();
		НоваяСтрока.Договор = СтрокаТек;
		НоваяСтрока.Подразделение = ПолучитьПодразделение(СтрокаТек);
		НоваяСтрока.Коэффициент = 1;
	КонецЦикла;
	ЕстьИзмененияПравила = Истина;
КонецПроцедуры

