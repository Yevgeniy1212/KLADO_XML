

#Область СобытияФормы
#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	ДобавитьВТаблицуИнструментыКолонкиНаСервере();
	ЗаполнитьТаблицуИнструментыНаФормеИзОбъекта();
КонецПроцедуры

&НаКлиенте
Процедура ИнструментыДинамическаяПриИзменении(Элемент)
	РассчитатьСуммыИнструменты();
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	ЗаполнитьТаблицуИнструментыВОбъекте();
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьЦены(Команда)
	Для Каждого Стр Из ИнструментыДинамическая Цикл
		Стр.Цена	= ПолучитьЦенуНаРесурс(Договор, Стр.Инструмент);
		Стр.ЦенаПрошлогоПериода	= ПолучитьЦенуНаРесурс(Договор, Стр.Инструмент,"ЦенаПрошлогоПериода");
		Стр.ЦенаПрошлогоПериодаСоСтатьей103	= ПолучитьЦенуНаРесурс(Договор, Стр.Инструмент,"ЦенаПрошлогоПериодаСоСтатьей103");
		Стр.ЦенаПрошлогоПериодаСоцПакет	= ПолучитьЦенуНаРесурс(Договор, Стр.Инструмент,"ЦенаПрошлогоПериодаСоцПакет");
	КонецЦикла;		
	РассчитатьСуммыИнструменты();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьВТаблицуИнструментыКолонкиНаСервере()
	
	ИнструментыДинамическая.Очистить();
	
	ИнструментыДинамическаяЗначение = РеквизитФормыВЗначение("ИнструментыДинамическая");
	
	МассивРеквизитов = Новый Массив;
	
	Для Каждого Колонка Из ИнструментыДинамическаяЗначение.Колонки Цикл
		Если СтрНайти(Колонка.Имя,"КоличествоДин") <> 0 Или СтрНайти(Колонка.Имя,"СуммаДин") <> 0 Тогда 
			МассивРеквизитов.Добавить("ИнструментыДинамическая." + Колонка.Имя);        
		КонецЕсли;
	КонецЦикла;
	ИзменитьРеквизиты(,МассивРеквизитов);
	
	СписокНаУдаление = Новый Массив;
	
	Для Каждого КолонкаИнструмент Из ИнструментыДинамическаяЗначение.Колонки Цикл
		Если КолонкаИнструмент.Имя <> "Инструмент" 
			И КолонкаИнструмент.Имя <> "СтатьяЗатрат" 
			И КолонкаИнструмент.Имя <> "Цена" 
			И КолонкаИнструмент.Имя <> "Сумма" 
			И КолонкаИнструмент.Имя <> "ЦенаПрошлогоПериода" 
			И КолонкаИнструмент.Имя <> "СуммаПрошлогоПериода" 
			И КолонкаИнструмент.Имя <> "ЦенаПрошлогоПериодаСоСтатьей103"
			И КолонкаИнструмент.Имя <> "СуммаПрошлогоПериодаСоСтатьей103"
			И КолонкаИнструмент.Имя <> "ЦенаПрошлогоПериодаСоцПакет"
			И КолонкаИнструмент.Имя <> "СуммаПрошлогоПериодаСоцПакет" Тогда 
			СписокНаУдаление.Добавить(КолонкаИнструмент);			
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элем Из СписокНаУдаление Цикл
		ИнструментыДинамическаяЗначение.Колонки.Удалить(Элем);		
	КонецЦикла;
	
	ДатаИнструмента		= НачалоДня(Договор.ДатаНачала);
	
	Пока ДатаИнструмента <= Договор.ДатаОкончания Цикл
		
		ИмяКолонки	= "КоличествоДин" + Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд");
		ИнструментыДинамическаяЗначение.Колонки.Добавить(ИмяКолонки,,Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд"));
		
		ИмяКолонкиСумма	= "СуммаДин" + Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд");
		ИнструментыДинамическаяЗначение.Колонки.Добавить(ИмяКолонкиСумма,,"Сумма " + Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд"));
		
		ИмяКолонкиСумма	= "СуммаДинПрошПериод" + Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд");
		ИнструментыДинамическаяЗначение.Колонки.Добавить(ИмяКолонкиСумма,,"Сумма " + Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд"));
		
		ИмяКолонкиСумма	= "СуммаДинПрошПериод103" + Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд");
		ИнструментыДинамическаяЗначение.Колонки.Добавить(ИмяКолонкиСумма,,"Сумма " + Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд"));
		
		ИмяКолонкиСумма	= "СуммаДинПрошПериодСоц" + Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд");
		ИнструментыДинамическаяЗначение.Колонки.Добавить(ИмяКолонкиСумма,,"Сумма " + Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд"));
		
		ДатаИнструмента = ДатаИнструмента + 86400;
		
	КонецЦикла;	
	
	МассивРеквизитов.Очистить();
	
	Для Каждого Колонка ИЗ ИнструментыДинамическаяЗначение.Колонки Цикл
		Если СтрНайти(Колонка.Имя,"КоличествоДин") <> 0 Тогда
			НоваяКолонка = Новый РеквизитФормы(Колонка.Имя, Новый ОписаниеТипов("Число"), "ИнструментыДинамическая");
			МассивРеквизитов.Добавить(НоваяКолонка);
		КонецЕсли; 
		Если СтрНайти(Колонка.Имя,"СуммаДин") <> 0 Тогда
			НоваяКолонка = Новый РеквизитФормы(Колонка.Имя, Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(10,2)), "ИнструментыДинамическая");
			МассивРеквизитов.Добавить(НоваяКолонка);
		КонецЕсли;
	КонецЦикла;      
	ИзменитьРеквизиты(МассивРеквизитов);  
	ЗначениеВРеквизитФормы(ИнструментыДинамическаяЗначение, "ИнструментыДинамическая");
	
	//Элементы формы
	
	ЭлементыНаУдаление = Новый Массив;
	
	Для Каждого ЭлФормы Из Элементы Цикл
		Если СтрНайти(ЭлФормы.Имя,"ИнструментыДинамическаяГруппа") <> 0 Тогда 
			ЭлементыНаУдаление.Добавить(ЭлФормы);
		КонецЕсли;
		Если СтрНайти(ЭлФормы.Имя,"ИнструментыДинамическаяКоличествоДин") <> 0 Тогда 
			ЭлементыНаУдаление.Добавить(ЭлФормы);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлМассива Из ЭлементыНаУдаление Цикл
		Попытка
			Элементы.Удалить(ЭлМассива);
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	ЭлементИнструментыДинамическая = Элементы.ИнструментыДинамическая;
	
	ТекущийМесяц = Неопределено;
	ДатаИнструмента		= НачалоДня(Договор.ДатаНачала);
	
	Пока ДатаИнструмента <= Договор.ДатаОкончания Цикл
		
		Если ТекущийМесяц <> НачалоМесяца(ДатаИнструмента)  Тогда 
			
			НоваяГруппаФормы 				= Элементы.Добавить("ИнструментыДинамическаяГруппа"+Формат(ДатаИнструмента,"ДФ=ММгггг"), Тип("ГруппаФормы"), ЭлементИнструментыДинамическая);
			НоваяГруппаФормы.Заголовок		= Формат(ДатаИнструмента,"ДФ='MMMM yyyy'");
			НоваяГруппаФормы.ОтображатьВШапке	= Истина;
			НоваяГруппаФормы.Группировка	= ГруппировкаКолонок.Горизонтальная;
			
			ТекущийМесяц = НачалоМесяца(ДатаИнструмента);
			
		КонецЕсли;
		
		НовыйЭлементФормы 				= Элементы.Добавить("ИнструментыДинамическаяКоличествоДин"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд"), Тип("ПолеФормы"), НоваяГруппаФормы);
		НовыйЭлементФормы.Вид 			= ВидПоляФормы.ПолеВвода;
		НовыйЭлементФормы.ПутьКДанным 	= "ИнструментыДинамическая.КоличествоДин" + Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд");
		НовыйЭлементФормы.Заголовок		= Формат(ДатаИнструмента,"ДФ=д");
		НовыйЭлементФормы.Ширина		= 3;
		
		ДатаИнструмента = ДатаИнструмента + 86400;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуИнструментыНаФормеИзОбъекта()
	
	Для Каждого Стр Из Договор.Инструменты Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Инструмент", Стр.Инструмент);
		ПараметрыОтбора.Вставить("СтатьяЗатрат", Стр.СтатьяЗатрат);
		НайденныеСтроки = ИнструментыДинамическая.НайтиСтроки(ПараметрыОтбора);
		
		Для Каждого НайдСтрока Из НайденныеСтроки Цикл
			НайдСтрока["КоличествоДин"+Формат(Стр.ДатаПотребности,"ЧГ=; ДФ=ггггММдд")] = Стр.КоличествоЧасов;	
			НайдСтрока["СуммаДин"+Формат(Стр.ДатаПотребности,"ЧГ=; ДФ=ггггММдд")] = Стр.Сумма;
			НайдСтрока["СуммаДинПрошПериод"+Формат(Стр.ДатаПотребности,"ЧГ=; ДФ=ггггММдд")] = Стр.СуммаПрошлогоПериода;
			НайдСтрока["СуммаДинПрошПериод103"+Формат(Стр.ДатаПотребности,"ЧГ=; ДФ=ггггММдд")] = Стр.СуммаПрошлогоПериодаСоСтатьей103;
			НайдСтрока["СуммаДинПрошПериодСоц"+Формат(Стр.ДатаПотребности,"ЧГ=; ДФ=ггггММдд")] = Стр.СуммаПрошлогоПериодаСоцПакет;
			НайдСтрока.Сумма = НайдСтрока.Сумма + Стр.Сумма;
			НайдСтрока.СуммаПрошлогоПериода = НайдСтрока.СуммаПрошлогоПериода + Стр.СуммаПрошлогоПериода;
			НайдСтрока.СуммаПрошлогоПериодаСоСтатьей103 = НайдСтрока.СуммаПрошлогоПериодаСоСтатьей103 + Стр.СуммаПрошлогоПериодаСоСтатьей103;
			НайдСтрока.СуммаПрошлогоПериодаСоцПакет = НайдСтрока.СуммаПрошлогоПериодаСоцПакет + Стр.СуммаПрошлогоПериодаСоцПакет;
		КонецЦикла;
		
		Если НайденныеСтроки.Количество() = 0 Тогда 
			СтрНовая					= ИнструментыДинамическая.Добавить();
			СтрНовая.Инструмент			= Стр.Инструмент;
			СтрНовая.СтатьяЗатрат		= Стр.СтатьяЗатрат;
			СтрНовая["КоличествоДин"+Формат(Стр.ДатаПотребности,"ЧГ=; ДФ=ггггММдд")] = Стр.КоличествоЧасов;
			СтрНовая["СуммаДин"+Формат(Стр.ДатаПотребности,"ЧГ=; ДФ=ггггММдд")] = Стр.Сумма;
			СтрНовая["СуммаДинПрошПериод"+Формат(Стр.ДатаПотребности,"ЧГ=; ДФ=ггггММдд")] = Стр.СуммаПрошлогоПериода;
			СтрНовая["СуммаДинПрошПериод103"+Формат(Стр.ДатаПотребности,"ЧГ=; ДФ=ггггММдд")] = Стр.СуммаПрошлогоПериодаСоСтатьей103;
			СтрНовая["СуммаДинПрошПериодСоц"+Формат(Стр.ДатаПотребности,"ЧГ=; ДФ=ггггММдд")] = Стр.СуммаПрошлогоПериодаСоцПакет;
			СтрНовая.Цена				= Стр.Цена;
			СтрНовая.ЦенаПрошлогоПериода	= Стр.ЦенаПрошлогоПериода;
			СтрНовая.ЦенаПрошлогоПериодаСоСтатьей103				= Стр.ЦенаПрошлогоПериодаСоСтатьей103;
			СтрНовая.ЦенаПрошлогоПериодаСоцПакет				= Стр.ЦенаПрошлогоПериодаСоцПакет;
		КонецЕсли;		
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммыИнструменты()
	
	ОбщаяСумма = 0;
	
	Для Каждого Стр Из ИнструментыДинамическая Цикл
		
		ДатаИнструмента = НачалоДня(Договор.ДатаНачала);
		
		Стр.Сумма = 0;
		Стр.СуммаПрошлогоПериода = 0;
		Стр.СуммаПрошлогоПериодаСоСтатьей103 = 0;
		Стр.СуммаПрошлогоПериодаСоцПакет = 0;
		
		Пока ДатаИнструмента <= Договор.ДатаОкончания Цикл
			
			Стр["СуммаДин"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")] = Стр["КоличествоДин"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")] * Стр.Цена;
			Стр["СуммаДинПрошПериод"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")] = Стр["КоличествоДин"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")] * Стр.ЦенаПрошлогоПериода;
			Стр["СуммаДинПрошПериод103"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")] = Стр["КоличествоДин"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")] * Стр.ЦенаПрошлогоПериодаСоСтатьей103;
			Стр["СуммаДинПрошПериодСоц"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")] = Стр["КоличествоДин"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")] * Стр.ЦенаПрошлогоПериодаСоцПакет;
			Стр.Сумма = Стр.Сумма + Стр["СуммаДин"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")]; 
			Стр.СуммаПрошлогоПериода = Стр.СуммаПрошлогоПериода + Стр["СуммаДинПрошПериод"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")];
			Стр.СуммаПрошлогоПериодаСоСтатьей103 = Стр.СуммаПрошлогоПериодаСоСтатьей103 + Стр["СуммаДинПрошПериод103"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")];
			Стр.СуммаПрошлогоПериодаСоцПакет = Стр.СуммаПрошлогоПериодаСоцПакет + Стр["СуммаДинПрошПериодСоц"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")];
			
			ДатаИнструмента = ДатаИнструмента + 86400;
			
		КонецЦикла;
		
		ОбщаяСумма = ОбщаяСумма + Стр.Сумма;
		
	КонецЦикла;
	
	Элементы.ИнструментыДинамическаяСумма.ТекстПодвала = Формат(ОбщаяСумма, "ЧДЦ=2");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуИнструментыВОбъекте()
	
	ДогОб = Договор.ПолучитьОбъект();
	
	ДогОб.Инструменты.Очистить();
	
	Для Каждого Стр Из ИнструментыДинамическая Цикл
		
		ДатаИнструмента = НачалоДня(ДогОб.ДатаНачала);
		
		Пока ДатаИнструмента <= ДогОб.ДатаОкончания Цикл
			
			Если Стр["КоличествоДин"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")] > 0 Тогда 
				
				СтрОбъекта 						= ДогОб.Инструменты.Добавить();
				СтрОбъекта.Инструмент 			= Стр.Инструмент;
				СтрОбъекта.СтатьяЗатрат			= Стр.СтатьяЗатрат;
				СтрОбъекта.ДатаПотребности		= ДатаИнструмента;
				СтрОбъекта.КоличествоЧасов		= Стр["КоличествоДин"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")];
				СтрОбъекта.Сумма				= Стр["СуммаДин"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")];
				СтрОбъекта.СуммаПрошлогоПериода	= Стр["СуммаДинПрошПериод"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")];
				СтрОбъекта.СуммаПрошлогоПериодаСоСтатьей103	= Стр["СуммаДинПрошПериод103"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")];
				СтрОбъекта.СуммаПрошлогоПериодаСоцПакет		= Стр["СуммаДинПрошПериодСоц"+Формат(ДатаИнструмента,"ЧГ=; ДФ=ггггММдд")];
				СтрОбъекта.Цена					= ?(СтрОбъекта.КоличествоЧасов=0,0,СтрОбъекта.Сумма/СтрОбъекта.КоличествоЧасов);
				СтрОбъекта.ЦенаПрошлогоПериода	= ?(СтрОбъекта.КоличествоЧасов=0,0,СтрОбъекта.СуммаПрошлогоПериода/СтрОбъекта.КоличествоЧасов);
				СтрОбъекта.ЦенаПрошлогоПериодаСоСтатьей103	= ?(СтрОбъекта.КоличествоЧасов=0,0,СтрОбъекта.СуммаПрошлогоПериодаСоСтатьей103/СтрОбъекта.КоличествоЧасов);
				СтрОбъекта.ЦенаПрошлогоПериодаСоцПакет		= ?(СтрОбъекта.КоличествоЧасов=0,0,СтрОбъекта.СуммаПрошлогоПериодаСоцПакет/СтрОбъекта.КоличествоЧасов);
				
			КонецЕсли;
			
			ДатаИнструмента = ДатаИнструмента + 86400;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ДогОб.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЦенуНаРесурс(Договор, Ресурс, ВидЦеныПерсонала = "Цена")  
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПлановыеЦеныНаРесурсыСрезПоследних.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ПлановыеЦеныНаРесурсы.СрезПоследних(
		|			&ДатаРасчета,
		|			Организация = &Организация
		|				И Ресурс = &Ресурс
		|				И Сценарий = &Сценарий) КАК ПлановыеЦеныНаРесурсыСрезПоследних";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,".Цена","."+ВидЦеныПерсонала);
	
	Запрос.УстановитьПараметр("ДатаРасчета",	Договор.ДатаОкончания);
	Запрос.УстановитьПараметр("Организация",	Договор.Организация);
	Если ТипЗнч(Ресурс) = Тип("СправочникСсылка.Персонал") Тогда 
		Запрос.УстановитьПараметр("Ресурс",		Ресурс.Должность);
	Иначе 
		Запрос.УстановитьПараметр("Ресурс",		Ресурс);
	КонецЕсли;
	Запрос.УстановитьПараметр("Сценарий",		Договор.Сценарий);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Цена
	КонецЦикла;
	
	Возврат 0

КонецФункции

#КонецОбласти
