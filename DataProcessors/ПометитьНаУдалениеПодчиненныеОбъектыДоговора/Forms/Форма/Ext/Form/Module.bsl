
&НаСервере
Процедура ЗаполнитьПомеченнымиНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Договоры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Договоры КАК Договоры
		|ГДЕ
		|	Договоры.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если СписокДоговоры.НайтиПоЗначению(ВыборкаДетальныеЗаписи.Ссылка) = Неопределено Тогда
			СписокДоговоры.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПомеченными(Команда)
	ЗаполнитьПомеченнымиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСсылки(Команда)
	ОчиститьСообщения();
	УдалитьСсылкиНаСервере();
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ссылки на договоры удалены.");
КонецПроцедуры

&НаСервере
Процедура УдалитьСсылкиНаСервере()
	//пометить задачи
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Задачи.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Задачи КАК Задачи
	               |ГДЕ
	               |	Задачи.Владелец В (&СписокДоговоры)";
	Запрос.УстановитьПараметр("СписокДоговоры", СписокДоговоры);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗадачаОб = Выборка.Ссылка.ПолучитьОбъект();
		Задачаоб.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
	
	//удаление строк распределения из ДТ2-4
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДоговорыРаспределения.Ссылка КАК Ссылка,
	               |	ДоговорыРаспределения.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	Справочник.Договоры.Распределения КАК ДоговорыРаспределения
	               |ГДЕ
	               |	ДоговорыРаспределения.Договор В(&СписокДоговоры)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтроки УБЫВ
	               |ИТОГИ ПО
	               |	Ссылка";
	Запрос.УстановитьПараметр("СписокДоговоры", СписокДоговоры);
	ВыборкаДоговор = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДоговор.Следующий() Цикл
		ДоговорОб = ВыборкаДоговор.Ссылка.ПолучитьОбъект();
		Выборка = ВыборкаДоговор.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаРаспределения = ДоговорОб.Распределения[Выборка.НомерСтроки-1];
			ДоговорОб.Распределения.Удалить(СтрокаРаспределения);
		КонецЦикла;
		ДоговорОб.СуммаРаспределенная = ДоговорОб.Распределения.Итог("Сумма");
		ДоговорОб.Записать();
	КонецЦикла;
		
	//удалений соответствий
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СоответствиеДоговоров.Организация КАК Организация,
	               |	СоответствиеДоговоров.Договор КАК Договор,
	               |	СоответствиеДоговоров.ДоговорИзУчетнойСистемы КАК ДоговорИзУчетнойСистемы
	               |ИЗ
	               |	РегистрСведений.СоответствиеДоговоров КАК СоответствиеДоговоров
	               |ГДЕ
	               |	СоответствиеДоговоров.Договор В(&СписокДоговоры)";
	Запрос.УстановитьПараметр("СписокДоговоры", СписокДоговоры);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МЗ = РегистрыСведений.СоответствиеДоговоров.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МЗ, Выборка);
		МЗ.Удалить();
	КонецЦикла;
	
	//удаление строк из бюджета
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	БюджетДоходы.Ссылка КАК Ссылка,
	               |	БюджетДоходы.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	Документ.Бюджет.Доходы КАК БюджетДоходы
	               |ГДЕ
	               |	БюджетДоходы.Договор В(&СписокДоговоры)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтроки УБЫВ
	               |ИТОГИ ПО
	               |	Ссылка";
	Запрос.УстановитьПараметр("СписокДоговоры", СписокДоговоры);
	ВыборкаИтог = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаИтог.Следующий() Цикл
		ДокОб = ВыборкаИтог.Ссылка.ПолучитьОбъект();
		Выборка = ВыборкаИтог.Выбрать();
		Пока Выборка.Следующий() Цикл
			Стр = ДокОб.Доходы[Выборка.НомерСтроки-1];
			ДокОб.Доходы.удалить(Стр);
		КонецЦикла;
		ДокОб.Записать(РежимЗаписиДокумента.Запись);
		Если Докоб.Проведен тогда
			ДокОб.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	БюджетРасходы.Ссылка КАК Ссылка,
	               |	БюджетРасходы.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	Документ.Бюджет.Расходы КАК БюджетРасходы
	               |ГДЕ
	               |	БюджетРасходы.Договор В(&СписокДоговоры)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтроки УБЫВ
	               |ИТОГИ ПО
	               |	Ссылка";
	Запрос.УстановитьПараметр("СписокДоговоры", СписокДоговоры);
	ВыборкаИтог = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаИтог.Следующий() Цикл
		ДокОб = ВыборкаИтог.Ссылка.ПолучитьОбъект();
		Выборка = ВыборкаИтог.Выбрать();
		Пока Выборка.Следующий() Цикл
			Стр = ДокОб.Расходы[Выборка.НомерСтроки-1];
			ДокОб.Расходы.удалить(Стр);
		КонецЦикла;
		ДокОб.Записать(РежимЗаписиДокумента.Запись);
		Если Докоб.Проведен тогда
			ДокОб.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


